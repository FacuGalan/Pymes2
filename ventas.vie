#include "Fivewin.ch"
#include "xbrowse.ch"
#include "Tdolphin.ch"
#include "FGet.ch"
************************************************************************************************
** VENTAS
************************************************************************************************
MEMVAR oApp
STATIC oWnd1, nTipoEmp, nTipoFac, nTipoDoc, cVentana, oGet, oQryArt, oQryCli, oQryPar, oQryDet, oQryRub,;
       oBrw, oSay1,oSay3,cRemitos, oDlg,nCodCli,nTarjeta,nefectivo,oQryPun,nDescArt,nDescArt1,nLista,nConIva,;
       nEfecti, nTarjet, nTransfe, nCheque, nCtaCte, nCuotas, nPuntoVta, cLetra,nLisPre,cObserva,nPorIva,cNumComp,;
       cDetArt, nDescuGen, nCodIva, nTotal, nForPag, nRubro, nDeuda,oQryChe,mpagado,manticipo,dFecha,cVendedor,nTipoCli,;
       nBorrarPend,lUsoPedido

PROCEDURE Ventas() 
LOCAL oBot := ARRAY(8), hHand , aTipoIva, aTipoDoc, cNomCli:=SPACE(30), cDireccion:=SPACE(30), cLocalidad:=SPACE(30),;
      cCUIT:=SPACE(13), nCodArt:=0, nCantidad:=0, nPunit:=0, nPtotal:=0, nNeto:=0, ;
      nIVA:=0, dFecha,  oFontLetra, oFontTotal,cDescu,oSay2,oQryUsua,aVendedores,;
      nNumFac:=0, nDNI := 0, nTiArt,oQryIvaT, cRubNom := SPACE(30),nIngBru:=0,nIngBruP:=0
 cVentana := PROCNAME()     
 IF ASCAN(oApp:aVentanas,cVentana) > 0
     hHand := ASCAN(oApp:aVentanas,cVentana)
     oApp:oWnd:Select(hHand)
     oApp:oWnd:oWndClient:aWnd[hHand]:Restore()
     RETURN
  ENDIF
  AADD(oApp:aVentanas,cVentana)
oQryUsua:= oApp:oServer:Query("SELECT codigo,nombre FROM ge_"+oApp:cId+"vendedores ORDER BY nombre")
IF oApp:usua_es_vendedor
   cVendedor:= oApp:usuario
ELSE 
   cVendedor:= SPACE(50)
ENDIF
aVendedores:= {}
AADD(aVendedores,cVendedor)
oQryUsua:GoTop()
DO WHILE !oQryUsua:Eof()
   AADD(aVendedores,oQryUsua:nombre)
   oQryUsua:Skip()
ENDDO
manticipo:= 0
nCodCli:=0
oGet := ARRAY(38) 
aTipoiva  := {;
"1 IVA Responsable Inscripto",;
"2 IVA Responsable no Inscripto",;
"3 IVA no Responsable",;
"4 IVA Sujeto Exento",;
"5 Consumidor Final",;
"6 Responsable Monotributo",;
"7 Sujeto no Categorizado",;
"8 Proveedor del Exterior",;
"9 Cliente del Exterior",;
"10  IVA Liberado – Ley Nº 19.640",;
"11  IVA Responsable Inscripto – Agente de Percepción",;
"12  Pequeño Contribuyente Eventual",;
"13  Monotributista Social",;
"14  Pequeño Contribuyente Eventual Social"}
  
  
oApp:oServer:Execute("";
    + "CREATE TEMPORARY TABLE IF NOT EXISTS ventas_det_H ";
    +"( `id` INT(6) NOT NULL AUTO_INCREMENT, ";
    +"`CODART` bigint(13) NOT NULL,";  
    +"`DETART` VARCHAR(50) NOT NULL,";
    +"`CANTIDAD` DECIMAL(8,3) DEFAULT '0',";
    +"`PUNIT` DECIMAL(10,2) DEFAULT '0.00', ";
    +"`NETO`  DECIMAL(10,2) DEFAULT '0.00', ";
    +"`DESCUENTO`  DECIMAL(10,2) DEFAULT '0.00', ";
    +"`DESCUP`  DECIMAL(5,2) DEFAULT '0.00', ";
    +"`STOTAL`  DECIMAL(10,2) DEFAULT '0.00', ";
    +"`IVA` DECIMAL(10,2) DEFAULT '0.00', ";
    +"`CODIVA` INT(2) DEFAULT '0', ";
    +"`PTOTAL` DECIMAL(10,2) DEFAULT '0.00',";
    +"`PCOSTO` DECIMAL(10,2) DEFAULT '0.00',";
    +"PRIMARY KEY (`id`)) ENGINE=INNODB DEFAULT CHARSET=utf8")
    //+"`COSTO` DECIMAL(10,2) DEFAULT '0.00', PRIMARY KEY (`id`)) ENGINE=INNODB DEFAULT CHARSET=utf8")

oApp:oServer:NextResult()

oApp:oServer:Execute("";
    + "CREATE TEMPORARY TABLE IF NOT EXISTS precios_temp ";
    +"( `FORMAPAG` VARCHAR(50) NOT NULL, ";
    +"`INCREMENTO` DECIMAL(5,2) DEFAULT '0.00',";
    +"`IMPORTE` DECIMAL(10,2) DEFAULT '0.00' ";
    +") ENGINE=INNODB DEFAULT CHARSET=utf8")

oApp:oServer:NextResult()
oApp:oServer:Execute("TRUNCATE precios_temp")

oApp:oServer:Execute("INSERT INTO precios_temp (formapag,incremento,importe) "+;
                     "(SELECT nombre,incremento,0 FROM ge_"+oApp:cId+"forpag )")

oQryDet :=oApp:oServer:Query("SELECT * FROM ventas_det_H")
oQryDet:ZAP()

aTipoDoc := {"Factura","Nota de Débito","Nota de Crédito","Presupuesto","Remito"}
nTipoDoc := 1 
oQryRub :=oApp:oServer:Query("SELECT * FROM ge_"+oApp:cId+"rubros")
oQryPar := oApp:oServer:Query("SELECT * FROM ge_"+oApp:cId+"parametros")
oQryPun := oApp:oServer:Query("SELECT * FROM ge_"+oApp:cId+"punto WHERE ip = "+ClipValue2Sql(oApp:cip))
nTipoEmp := oQryPar:coniva
nTipoFac := oQryPar:factura
nTipoCli := 1
nLista:=1
nPuntoVta := oQryPun:caja
oQryCli := oApp:oServer:Query("SELECT * FROM ge_"+oApp:cId+"clientes ORDER BY codigo")
oQryIvaT:= oApp:oServer:Query("SELECT * FROM ge_"+oApp:cId+"ivas ORDER BY tasa")

dFecha:= DATE()
nLisPre:=1
cObserva:=SPACE(100)
nPorIva:=21.00
nCodIva:= 5
nTiArt:=1
nDescuGen:=0
cDetArt:=SPACE(50)
nTotal:=0
nForPag:=2
nRubro := 1
nDeuda:= 0
nDescArt:=0
nDescArt1:=0
cRemitos:= SPACE(20)
cRubNom := SPACE(30)
cDescu:= "Descuento:"
nConIva:=1
nBorrarPend:=0
lUsoPedido:=.f.


 
      
  //FUENTES -----------------------------------------------
  DEFINE FONT oFontLetra NAME "ARIAL" SIZE 35,60
  DEFINE FONT oFontTotal NAME "ARIAL" SIZE 12,30
  cLetra := IF(oApp:tipo_iva<>6,"A","C")
  DEFINE WINDOW oWnd1 MDICHILD TITLE "Facturacion por ventas" OF oApp:oWnd NOZOOM ICON oApp:oIco
   
    *oWnd1:bGotFocus := { || oDlg:SetFocus}
    oWnd1:bResized := { || Incrusta( oWnd1, oDlg, .t.) }

    //DLG -----------------------------------------------
    DEFINE DIALOG oDlg RESOURCE "VENTAS" OF oWnd1
    //Fechas y tipo de Comprobante
    REDEFINE GET oGet[01] VAR dFecha ID 176  OF oDlg 
    REDEFINE COMBOBOX oGet[02] VAR nTipoDoc ITEMS aTipoDoc ID 177 OF oDlg;
             ON CHANGE(oGet[08]:SetFocus())
    REDEFINE SAY oSay1    VAR cLetra  ID 180  OF oDlg FONT oFontLetra
    REDEFINE SAY oSay2    VAR cDescu  ID 999 OF oDlg
    //Numero de comprobante 
    REDEFINE GET oGet[05] VAR nPuntoVta ID 181 OF oDlg WHEN (.f.) PICTURE "9999"
    REDEFINE GET oGet[06] VAR cNumComp   ID 182 OF oDlg WHEN (.f.) PICTURE "@!"
    REDEFINE COMBOBOX oGet[36] VAR cVendedor ID 4011 OF oDlg ITEMS aVendedores WHEN(oApp:modifica_vend)

    //Tipo de cliente
    REDEFINE RADIO oGet[07] VAR nTipoCli ID 183, 184 OF oDlg ;
             ON CHANGE IF(nTipoCli = 1, (nCodCli := 0, oGet[08]:SetFocus()), ;
                          (oGet[08]:cText := 0, oGet[09]:cText := SPACE(50), ;
                           oGet[10]:cText := SPACE(50),oGet[11]:cText := SPACE(50),;
                           oGet[12]:cText := SPACE(13), oGet[22]:cText :=0, oGet[13]:Set(5),oGet[09]:SetFocus() ) )

    //CLIENTE
    REDEFINE GET oGet[08] VAR nCodCli ID 185 OF oDlg PICTURE "999999" WHEN(nTipoCli=1 .and. !lUsoPedido) ;
             ACTION (oGet[08]:cText:= 0, ValidaCli())  BITMAP "BUSC1" VALID (ValidaCli()) 
    REDEFINE GET oGet[09] VAR cNomCli ID 186 OF oDlg PICTURE "@!"  WHEN(nTipoCli=2)
    REDEFINE GET oGet[10] VAR cDireccion ID 187  OF oDlg PICTURE "@!" WHEN(nTipoCli=2)
    REDEFINE GET oGet[11] VAR cLocalidad ID 188  OF oDlg PICTURE "@!" WHEN(nTipoCli=2)
    REDEFINE GET oGet[12] VAR cCUIT      ID 189 OF oDlg PICTURE "99-99999999-9" ;
             WHEN(nTipoCli=2) VALID(ValidaCuit(cCuit))
    REDEFINE GET oGet[22] VAR nDNI      ID 201 OF oDlg PICTURE "99999999" ;
             WHEN(nTipoCli=2)
    REDEFINE COMBOBOX oGet[13] VAR nConIva ID 190 OF oDlg WHEN(nTipoCli=2) ITEMS aTipoIva;
             ON CHANGE ValidaCli(.f.) 
    REDEFINE GET oGet[31] VAR nDescuGen ID 308 OF oDlg PICTURE "999.99" WHEN(nTipoDoc = 5)
    REDEFINE GET oGet[23] VAR cObserva  ID 307 OF oDlg PICTURE "@!"
    REDEFINE GET oGet[32] VAR nDeuda    ID 4001 OF oDlg PICTURE "99999999.99" WHEN(.F.)
    
    REDEFINE SAY oSay3 VAR cRemitos ID 4002 OF oDlg COLOR CLR_GREEN

    //TIPO ARTICULO
    REDEFINE RADIO oGet[24] VAR nTiArt  ID 300,301 OF oDlg;
             ON CLICK(nCodArt:=0        ,cDetArt:=SPACE(50),nCantidad:=0      ,nPunit:=0         ,nTotal:=0,;
                      oGet[14]:Refresh(),oGet[15]:Refresh(),oGet[16]:Refresh(),oGet[17]:Refresh(),oGet[18]:Refresh(),;
                                         oGet[25]:Refresh(),oGet[26]:Refresh(),oGet[27]:Refresh(),oGet[28]:Refresh())
    
    //Datos del articulo registrado
    REDEFINE GET oGet[14] VAR nCodArt   ID 191  OF oDlg PICTURE "99999999999999" WHEN(nTiArt=1);
             ACTION (oGet[14]:cText:= 0, ValidaArt())  BITMAP "BUSC1" VALID(ValidaArt())
    REDEFINE GET oGet[15] VAR cDetArt   ID 192  OF oDlg PICTURE "@!" WHEN(.F.)
    REDEFINE GET oGet[16] VAR nCantidad ID 193  OF oDlg PICTURE "999999.999"  VALID(nCantidad>0) WHEN(nTiArt=1)
    REDEFINE GET oGet[17] VAR nPunit    ID 194  OF oDlg PICTURE "9999999.999" WHEN(nTiArt=1 .and. oApp:modifica_precios)
    REDEFINE GET oGet[33] VAR nDescArt  ID 4003 OF oDlg PICTURE "999.99" WHEN(nTiArt=1 .and. oApp:modifica_descu)
    REDEFINE GET oGet[18] VAR nPtotal   ID 195  OF oDlg PICTURE "9999999.999" ;
            WHEN((oGet[18]:cText := nCantidad*nPunit-(nCantidad*nPunit)*(nDescArt/100)) = "-9" .and. nTiArt=1) 
            
    //Datos del articulo ocacional
    REDEFINE GET oGet[25] VAR cDetArt   ID 302  OF oDlg PICTURE "@!" WHEN(nTiArt=2)
    REDEFINE GET oGet[26] VAR nCantidad ID 304  OF oDlg PICTURE "999999.999"  VALID(nCantidad>0) WHEN(nTiArt=2)
    REDEFINE GET oGet[27] VAR nPunit    ID 305  OF oDlg PICTURE "9999999.999" WHEN(nTiArt=2);
                 VALID((oGet[28]:cText := nCantidad*nPunit-(nCantidad*nPunit)*(nDescArt1/100)) <> "xxx" )
    REDEFINE GET oGet[34] VAR nDescArt1  ID 4004 OF oDlg PICTURE "999.99" WHEN(nTiArt=2 .and. oApp:modifica_descu)
    REDEFINE GET oGet[28] VAR nPtotal   ID 306  OF oDlg PICTURE "9999999.999" WHEN(.f.)
    REDEFINE GET oGet[30] VAR nPorIva   ID 303  OF oDlg PICTURE "999.99" WHEN(nTiArt=2); 
             VALID((oQryIvaT:Seek(nPorIva,3) > 0) .and. ((nCodIva:=oQryIvaT:codigo) >0 ))
          

    //ingresos brutos 
    REDEFINE GET oGet[37] VAR nIngBruP  ID 4013 OF oDlg PICTURE "999.99" WHEN(.F.)
    REDEFINE GET oGet[38] VAR nIngBru   ID 4014 OF oDlg PICTURE "99999999.99" WHEN(.F.)
    //neto y IVA y total
    REDEFINE GET oGet[19] VAR nNeto  ID 198 OF oDlg PICTURE "99999999.99" WHEN(.F.)
    REDEFINE GET oGet[20] VAR nIVA   ID 199 OF oDlg PICTURE "99999999.99" WHEN(.F.)
    REDEFINE GET oGet[21] VAR nTotal ID 200 OF oDlg COLOR CLR_RED , CLR_YELLOW READONLY;
           PICTURE "999999999.99" FONT oFontTotal WHEN(.F.)
       
    //GRILLA ---------------------------------------------------------------      
    
    REDEFINE XBROWSE oBrw DATASOURCE oQryDet;
       COLUMNS "CODART","DETART","CANTIDAD","PUNIT","NETO","DESCUENTO","STOTAL","IVA","PTOTAL","CODIVA";
       HEADERS "Codigo","Detalle Articulo","Cantidad","Precio U","Neto","Dto./Rec.","Sub Total","IVA","Total","Tasa";
       FOOTERS ;
       SIZES 40,243,55,55,55,55,55,55,55;
    ID 197 OF oDlg AUTOSORT
    PintaBrw(oBrw,0)
    
    oBrw:bKeyDown := { |nKey| IF (nKey == VK_DELETE,(EliminarItem(),;
                                   nTotal:= oBrw:aCols[9]:nTotal,oGet[21]:Refresh()),)}
    oBrw:aCols[9]:nFooterTypE := AGGR_SUM
    oBrw:aCols[5]:nFooterTypE := AGGR_SUM
    oBrw:aCols[6]:nFooterTypE := AGGR_SUM
    oBrw:aCols[7]:nFooterTypE := AGGR_SUM
    oBrw:aCols[8]:nFooterTypE := AGGR_SUM
    oBrw:aCols[3]:lAutoSave := .t.
    oBrw:aCols[3]:nEditType := EDIT_GET 
    oBrw:aCols[3]:bOnPostEdit := {|oCol, xVal, nKey | CambiaCant(xval,,)} 
    IF oApp:modifica_precios
      oBrw:aCols[4]:lAutoSave := .t.
      oBrw:aCols[4]:nEditType := EDIT_GET 
      oBrw:aCols[4]:bOnPostEdit := {|oCol, xVal, nKey | CambiaCant(,,xval)} 
    ENDIF
    oBrw:aCols[10]:Hide()
    
    //BOTONES ---------------------------------------------------------------
    //+
    REDEFINE BUTTON oBot[1] ID 196 ACTION(AgregaItem(nTiArt),IF(nTiArt=1,oGet[14]:Setfocus(),oGet[25]:Setfocus()) ) ;
                   WHEN(((nCodArt>0 .and. nTiArt=1 .and. nPtotal>0) .or. (nTiArt=2 .and. cDetArt<>SPACE(30) .and. nPtotal>0)) .and. nCantidad > 0)
    //cancelar
    REDEFINE BUTTON oBot[2] ID 102 ACTION (LimpiarForm()) 
    //grabar
    REDEFINE BUTTON oBot[3] ID 103  ACTION Validaciones() WHEN(oBrw:aCols[9]:nTotal > 0 .and. IF(oApp:usar_remitos,.T.,IF(nTipoDoc=5,.F.,.T.)))
    //elegir factura para nota de credito
    REDEFINE BUTTON oBot[5] ID 4005 ACTION(ElegirFact()) WHEN(nTipoDoc=3 .or. nTipoDoc=1)
    //salir
    REDEFINE BUTTON oBot[4] ID 104 ACTION (oDlg:End()) CANCEL 
    //consultar precios 
    REDEFINE BUTTON oBot[6] ID 4012 ACTION (ConsultaPre(oGet),oBot[1]:SetFocus()) WHEN(nCodArt>0)
    //dejar factura pendiente
    REDEFINE BUTTON oBot[7] ID 105 ACTION(DejarPendiente(oGet)) WHEN(oBrw:aCols[9]:nTotal > 0 .and. IF(oApp:usar_remitos,.T.,IF(nTipoDoc=5,.F.,.T.)))
    //salir
    REDEFINE BUTTON oBot[8] ID 4015 ACTION (IF(CargarPedido(nCodCli),oBrw:SetFocus(),oGet[8]:SetFocus())) PROMPT "Pedido" WHEN(nCodCli>0)
    // F9 Para grabar             
    oDlg:bKeyDown = { | nKey, nFlags | IF(nKey==VK_F9,oBot[3]:Click,;
                                       IF(nKey==VK_F3,oBot[5]:Click,;
                                       IF(nKey==VK_F4,oBot[6]:Click,;
                                       .f.))) }
    //ACTiVACION   ---------------------------------------------------------------
    ACTIVATE DIALOG oDlg CENTER NOWAIT ON INIT oDlg:Move(0,0) VALID(oWnd1:End())
    ACTIVATE WINDOW oWnd1 ON INIT Incrusta( oWnd1, oDlg, .T.) VALID(cerrar())
RETURN 


*****************************************************************************************************************
****** TRAER PEDIDO PENDIENTE DEL CLIENTE 
STATIC FUNCTION CargarPedido(nCodCli)

IF oApp:oServer:Query("SELECT * FROM ge_"+oApp:cId+"pedidos WHERE codcli = "+ClipValue2Sql(nCodCli)):RecCount() = 0
   MsgStop("El cliente seleccionado no tiene un pedido cargado","Atencion!")
   RETURN .F. 
ENDIF
oApp:oServer:Execute("INSERT INTO ventas_det_H (codart,detart,cantidad,punit,neto,descuento,stotal,iva,codiva,ptotal,pcosto,descup) "+;
                   "(SELECT p.codart,p.detart,p.cantidad-p.faltantes,p.punit,p.neto,p.descu,p.neton,p.iva,p.codiva,p.importe,a.preciocos,p.descup "+;
                   "FROM ge_"+oApp:cId+"pedidos p LEFT JOIN ge_"+oApp:cId+"articu a ON a.codigo = p.codart WHERE p.codcli = "+ClipValue2Sql(nCodCli)+" AND "+;
                   "p.cantidad-p.faltantes > 0)")
                            
oQryDet:Refresh()
oBrw:Refresh()
oBrw:MakeTotals()
oGet[19]:cText := oBrw:aCols[7]:nTotal 
oGet[20]:cText := oBrw:aCols[8]:nTotal      
oGet[38]:cText := oBrw:aCols[7]:nTotal * (oGet[37]:value/100) 
oGet[21]:cText := oBrw:aCols[9]:nTotal + oGet[38]:value
lUsoPedido:=.t.
RETURN .T. 

*****************************************
** Cambiar pendientes
STATIC FUNCTION CambiaCant(n,nPorc,nPUnit,cNombre)
LOCAL base := oQryDet:GetRowObj(),nPrecio2,nDescuento,nPrecio1,nIvaPes,nSubTotal,nTotalcIva,oQryIva,nCodIva1,nIvaCalc
DEFAULT n := base:cantidad,nPorc:= base:descuento,nPUnit:= base:punit, cNombre:= base:detart
oQryIva := oApp:oServer:Query("SELECT tasa FROM ge_"+oApp:cId+"ivas WHERE codigo = " + ClipValue2Sql(base:codiva))
nCodIva1 := base:codiva
nPrecio2:= nPUnit * (n)
nDescuento:= nPrecio2 * (nPorc/100)
nPrecio1:= nPrecio2 - nDescuento
nIvaCalc := nPrecio2 - (nPrecio2 / (1 + oQryIva:tasa/100))
nIvaPes := nPrecio1 - (nPrecio1 / (1 + oQryIva:tasa/100))
nSubTotal:= nPrecio1 - nIvaPes
nTotalcIva := nPrecio1

base:descuento:= nPorc
base:cantidad := n 
base:ptotal:=nTotalcIva
base:neto:=nPrecio2-nIvaCalc
base:iva:=nIvaPes
base:descuento:=nDescuento
base:stotal:=nSubTotal
base:punit:= nPUnit
base:detart:= cNombre
oQryDet:oRow := base
oQryDet:Save()
oQryDet:Refresh()
oBrw:Refresh()
oBrw:Maketotals()
oGet[19]:cText := oBrw:aCols[7]:nTotal 
oGet[20]:cText := oBrw:aCols[8]:nTotal  
oGet[38]:cText := oBrw:aCols[7]:nTotal * (oGet[37]:value/100) * IF(nTipoDoc=3,-1,1)
oGet[21]:cText := oBrw:aCols[9]:nTotal + oGet[38]:value
oGet[19]:Refresh()
oGet[20]:Refresh()
oGet[21]:Refresh()
RETURN nil

************************************************************************************************************
******* DEJAR FACTURA PENDIENTE DE FACTURACION
STATIC FUNCTION DejarPendiente(oGet)
LOCAL nCliente:= IF(nTipoCli=1,oGet[8]:value,-1),nNumero

      oApp:oServer:Execute("INSERT INTO ge_"+oApp:cId+"ventas_encab_p (nombre_equipo,codcli,nombre,cuit,dni,direccion,localidad,coniva,importe) "+;
                           "VALUES("+ClipValue2Sql(oApp:cNombreEquipo)+","+ClipValue2Sql(nCliente)+","+ClipValue2Sql(oGet[9]:cText)+","+;
                            ClipValue2Sql(oGet[12]:cText)+","+ClipValue2Sql(oGet[22]:cText)+","+;
                            ClipValue2Sql(oGet[10]:cText)+","+ClipValue2Sql(oGet[11]:cText)+","+ClipValue2Sql(nConIva)+","+;
                            ClipValue2Sql(nTotal)+")")
      nNumero:= oApp:oServer:Query("SELECT MAX(id) AS ultimo FROM ge_"+oApp:cId+"ventas_encab_p"):ultimo
      oApp:oServer:Execute("INSERT INTO ge_"+oApp:cId+"ventas_det_p "+;
                        " (numven,codart,detart,cantidad,punit,codcli,importe,neto,iva,codiva,neton,descu,pcosto) "+;
                        " (SELECT "+ClipValue2Sql(nNumero)+",codart,detart,cantidad,punit,"+ClipValue2Sql(nCliente)+","+;
                        " ptotal, stotal,iva,codiva,neto,descuento,pcosto FROM ventas_det_H)")
LimpiarForm()
RETURN nil
*******************************************************************************************
******* MOSTRAR REMITOS A FACTURAR
STATIC FUNCTION ConsultaPre(oGet)
LOCAL oDlg1,oBrwRem,oQryRem,oBot1,oBot2,acor:=ARRAY(4),mRta:=.f.


oApp:oServer:Execute("UPDATE precios_temp SET importe = "+;
                    "(SELECT "+IF(nLista=1,"precioven","reventa")+" FROM ge_"+oApp:cId+"articu WHERE codigo = "+ClipValue2Sql(oQryArt:codigo)+")"+;
                    "*(1+(incremento/100))")

oQryRem:= oApp:oServer:Query("SELECT * FROM precios_temp")

DEFINE DIALOG oDlg1 TITLE "Lista de precios segun forma de pago "+ALLTRIM(cDetArt) OF oWnd1 FROM 05,15 TO 21,78
    oDlg1:lHelpIcon:=.f.

@ 05,05 XBROWSE oBrwRem DATASOURCE oQryRem;
                 COLUMNS "formapag","incremento","importe";    
                 HEADERS "Forma","Incr %","Precio";
                 SIZES 308,50,70;
         OF oDlg1 SIZE 240,97 PIXEL 
oQryRem:bOnChangePage := {|| oBrwRem:Refresh() }
oBrwRem:CreateFromcode()
PintaBrw(oBrwRem,0)

oBrwRem:bKeyDown = { | nKey, nFlags | IF(nKey==13,oBot1:Click,.f.) }

acor := AcepCanc(oDlg1)
@ acor[1],acor[2] BUTTON oBot1 PROMPT "&Aceptar" OF oDlg1 SIZE 30,10 ;
           ACTION ((mrta := .t.), oDlg1:End() ) PIXEL DEFAULT 
@ acor[3],acor[4] BUTTON oBot2 PROMPT "&Cancelar" OF oDlg1 SIZE 30,10 ;
           ACTION ((mrta := .f.), oDlg1:End() ) PIXEL CANCEL
ACTIVATE DIALOG oDlg1 CENTER ON INIT oBrwRem:SetFocus()

IF !mRta
  RETURN nil 
ENDIF

oGet[17]:cText:= oQryRem:importe 

RETURN nil

************************************************************************************
***** Elegir factura para nota de credito
STATIC FUNCTION ElegirFact()
LOCAL oGet1, oGet2, oGet3, oGet4, oBot1, oBot2, oDlg1, oFont, mnumfac, ;
      mrta := .f., aCor, base, marchi, mtotal, mdescp := 0, mdeuda := 0,;
      aTipo  := {"Factura","Factur x Rem."}, oRep,;
      aTip1  := {"FC","FR"}, mtipo := 1, mletra := "A", mprefijo := 3,;
      mnumero := 0,oQryFac, oQryPend,oBrwPend
DEFINE FONT oFont NAME "TAHOMA" SIZE 0,-11.5   

IF nTipoDoc = 3   
    DEFINE DIALOG oDlg1 TITLE "Elejir factura para nota de credito" FROM 03,20 TO 14,60 OF oApp:oWnd FONT oFont
       acor := AcepCanc(oDlg1)
       
       @ 07, 05 SAY "Tipo Docum.:" OF oDlg1 PIXEL RIGHT SIZE 40,10
       @ 22, 05 SAY "Letra:"       OF oDlg1 PIXEL RIGHT SIZE 40,10
       @ 37, 05 SAY "Prefijo:"     OF oDlg1 PIXEL RIGHT SIZE 40,10
       @ 52, 05 SAY "Numero:"      OF oDlg1 PIXEL RIGHT SIZE 40,10
       
       @ 05, 50 COMBOBOX oGet1 VAR mtipo ITEMS aTipo OF oDlg1 SIZE 79,50 PIXEL
       @ 20, 50 GET      oGet2 VAR mletra PICTURE "@!" OF oDlg1 SIZE 20,10 PIXEL;
                         VALID(mletra$"ABCX")
       @ 35, 50 GET      oGet3 VAR mprefijo PICTURE "9999" OF oDlg1 PIXEL RIGHT
       @ 50, 50 GET      oGet4 VAR mnumero  PICTURE "99999999" OF oDlg1 PIXEL RIGHT
       
       @ acor[1],acor[2] BUTTON oBot1 PROMPT "&Imprimir" OF oDlg1 SIZE 30,10 ;
               ACTION ((mrta := .t.), oDlg1:End() ) PIXEL
       @ acor[3],acor[4] BUTTON oBot2 PROMPT "&Cancelar" OF oDlg1 SIZE 30,10 ;
               ACTION ((mrta := .f.), oDlg1:End() ) PIXEL
    ACTIVATE DIALOG oDlg1 CENTER ON INIT oGet1:SetFocus()
    IF !mrta
       RETURN nil
    ENDIF
    oQryFac:= oApp:oServer:Query("SELECT * FROM ge_"+oApp:cId+"ventas_encab WHERE ticomp = "+ClipValue2Sql(aTip1[mtipo])+;
                                 "AND letra = "+ClipValue2Sql(mletra)+;
                                 "AND numcomp = "+ClipValue2Sql(STRTRAN(STR(mprefijo,4)+"-"+STR(mnumero,8)," ","0")))
    IF oQryFac:RecCount() = 0
       MsgStop("La factura no existe","Atencion!")
       RETURN nil 
    ENDIF

    oGet[08]:cText:= oQryFac:codcli
    ValidaCli()                                   

    oApp:oServer:Execute("TRUNCATE ventas_det_H")
    oApp:oServer:Execute("INSERT INTO ventas_det_H (codart,detart,cantidad,punit,neto,descuento,descup,stotal,iva,codiva,ptotal,pcosto) "+;
                         "(SELECT codart,detart,cantidad,punit,neton,descu,0,neto,iva,codiva,importe,pcosto FROM ge_"+oApp:cId+"ventas_det "+;
                         "WHERE nrofac = "+ClipValue2Sql(oQryFac:ticomp+oQryFac:letra+oQryFac:numcomp)+")")
ENDIF

IF nTipoDoc= 1
    oQryPend:= oApp:oServer:Query("SELECT * FROM ge_"+oApp:cId+"ventas_encab_p")
    DEFINE DIALOG oDlg1 TITLE "Listado de facturas pendientes" OF oWnd1 FROM 05,15 TO 21,78
        oDlg1:lHelpIcon:=.f.

    @ 05,05 XBROWSE oBrwPend DATASOURCE oQryPend;
                     COLUMNS "nombre_equipo","nombre","importe";    
                     HEADERS "Cargada en","Cliente","Importe";
                     SIZES 140,218,70;
             OF oDlg1 SIZE 240,97 PIXEL 
    oQryPend:bOnChangePage := {|| oBrwPend:Refresh() }
    oBrwPend:CreateFromcode()
    PintaBrw(oBrwPend,0)

    oBrwPend:bKeyDown = { | nKey, nFlags | IF(nKey==13,oBot1:Click,.f.) }

    acor := AcepCanc(oDlg1)
    @ acor[1],acor[2] BUTTON oBot1 PROMPT "&Aceptar" OF oDlg1 SIZE 30,10 ;
               ACTION ((mrta := .t.), oDlg1:End() ) PIXEL DEFAULT 
    @ acor[3],acor[4] BUTTON oBot2 PROMPT "&Cancelar" OF oDlg1 SIZE 30,10 ;
               ACTION ((mrta := .f.), oDlg1:End() ) PIXEL CANCEL
    ACTIVATE DIALOG oDlg1 CENTER ON INIT oBrwPend:SetFocus()

    IF !mrta
       RETURN nil
    ENDIF

    IF oQryPend:codcli > 0
       oGet[08]:cText= oQryPend:codcli
       ValidaCli()
    ELSE 
       oGet[08]:cText:= 0
       oGet[09]:cText:= oQryPend:nombre 
       oGet[10]:cText:= oQryPend:direccion
       oGet[11]:cText:= oQryPend:localidad 
       oGet[12]:cText:= oQryPend:cuit 
       oGet[22]:cText:= oQryPend:dni
       oGet[13]:Set(oQryPend:coniva)
    ENDIF
    oApp:oServer:Execute("TRUNCATE ventas_det_H")
    oApp:oServer:Execute("INSERT INTO ventas_det_H (codart,detart,cantidad,punit,neto,descuento,descup,stotal,iva,codiva,ptotal,pcosto) "+;
                         "(SELECT codart,detart,cantidad,punit,neton,descu,0,neto,iva,codiva,importe,pcosto FROM ge_"+oApp:cId+"ventas_det_p "+;
                         "WHERE numven = "+ClipValue2Sql(oQryPend:id)+")")
    nBorrarPend:= oQryPend:id
ENDIF
oQryDet:Refresh()
oBrw:Refresh()
oBrw:Maketotals()
oGet[19]:cText := oBrw:aCols[7]:nTotal 
oGet[20]:cText := oBrw:aCols[8]:nTotal 
oGet[38]:cText := oBrw:aCols[7]:nTotal * (oGet[37]:value/100) * IF(nTipoDoc=3,-1,1)
oGet[21]:cText := oBrw:aCols[9]:nTotal + oGet[38]:value

RETURN nil

************************************************************************************
** Vaciar el formulario despues de grabar o cancelar
STATIC FUNCTION LimpiarForm()
oQryDet:Zap()
oBrw:Refresh()
oBrw:Maketotals()
oGet[19]:cText := oBrw:aCols[7]:nTotal 
oGet[20]:cText := oBrw:aCols[8]:nTotal      
oGet[38]:cText := oBrw:aCols[7]:nTotal * (oGet[37]:value/100) * IF(nTipoDoc=3,-1,1)
oGet[21]:cText := oBrw:aCols[9]:nTotal + oGet[38]:value
oGet[19]:Refresh()
oGet[20]:Refresh()
oGet[21]:Refresh()
oGet[08]:cText := 1
oGet[02]:SetFocus()
nBorrarPend:=0
RETURN nil


************************************************************************************
** Agregar item a la venta
STATIC FUNCTION AgregaItem(nTiArt)
LOCAL oQryIva, nTotalcIva, nIvaPes, nCodIva1, nSubTotal, nDescuento,;
      i,nCantidad,nPrecio1,nPrecio2,lRta:=.f.,nIvaCalc:=0,nDescMar

IF nTiArt = 1
   IF oQryPun:pidestock = 1 .and. oQryArt:stockact-oGet[16]:value <= 0
      lRta:=MsgYesNo("El articulo que esta facturando esta fuera de stock,"+CHR(10)+;
              "verifique su disponibilidad ¿Desea continuar?"+CHR(10)+;
              "Stock actual: "+ALLTRIM(STR(oQryArt:stockact)),"Atencion")
      IF !lRta 
         RETURN .f.
      ENDIF
  ENDIF
  IF oQryPun:pidestock = 2 .and. oQryArt:stockact-oGet[16]:value <= 0
     MsgStop("No puede facturar el articulo seleccionado porque no esta en stock"+CHR(10)+;
     	     "Stock actual: "+ALLTRIM(STR(oQryArt:stockact)),"Atencion")
     RETURN .F.
  ENDIF


   nDescMar:= oApp:oServer:Query("SELECT descuento FROM ge_"+oApp:cId+"desccli WHERE codmar ="+ClipValue2Sql(oQryArt:marca)+" AND "+;
                                 "codcli ="+ClipValue2Sql(oGet[8]:value)):descuento
   IF EMPTY(nDescMar) 
      nDescMar :=0
   ENDIF
   oQryIva := oApp:oServer:Query("SELECT tasa FROM ge_"+oApp:cId+"ivas WHERE codigo = " + ClipValue2Sql(oQryArt:iva))
   nCodIva1 := oQryArt:iva
   nPrecio2:= oGet[17]:value * oGet[16]:value
   nDescuento:= nPrecio2 * ( IF(nDescMar>0,nDescMar,IF(nDescArt>0,nDescArt,nDescuGen))/100)
   nPrecio1:= nPrecio2 - nDescuento
   nIvaCalc := nPrecio2 - (nPrecio2 / (1 + oQryIva:tasa/100))
   nIvaPes := nPrecio1 - (nPrecio1 / (1 + oQryIva:tasa/100))
   nSubTotal:= nPrecio1 - nIvaPes
   nTotalcIva := nPrecio1
   oApp:oServer:Execute("INSERT INTO ventas_det_H (codart,detart,cantidad,punit,ptotal,neto,iva,codiva,descuento,descup,stotal,pcosto) "+;
                         "VALUE ( "+ClipValue2Sql(oQryArt:codigo)+","+;
                                 ClipValue2Sql(oQryArt:nombre)+","+;
                                 ClipValue2Sql(oGet[16]:cText)+","+;
                                 ClipValue2Sql(oGet[17]:value)+","+;
                                 ClipValue2Sql(nTotalcIva)+","+;
                                 ClipValue2Sql(nPrecio2-nIvaCalc)+","+;
                                 ClipValue2Sql(nIvaPes)+","+;
                                 ClipValue2Sql(nCodIva1)+","+;
                                 ClipValue2Sql(nDescuento)+","+;
                                 ClipValue2Sql(IF(nDescArt>0,nDescArt,nDescuGen))+","+;
                                 ClipValue2Sql(nSubTotal)+","+;
                                 ClipValue2Sql(oQryArt:preciocos)+;
                         " )" ) 
ELSE
 nDescuento:=oGet[18]:Value * (IF(nDescArt1>0,nDescArt1,nDescuGen)/100)
 nSubTotal:=oGet[18]:Value - nDescuento
 nIvaPes := nSubTotal * (nPorIva/100)
 nTotalcIva := nSubTotal + nIvaPes
 nCodIva := IF(nPorIva=0,3,nCodIva)
 oApp:oServer:Execute("INSERT INTO ventas_det_H (codart,detart,cantidad,punit,ptotal,neto,iva,codiva,descuento,descup,stotal) "+;
                      "VALUE ( 0,"+;
                                 ClipValue2Sql(oGet[15]:value)+","+;
                                 ClipValue2Sql(oGet[16]:value)+","+;
                                 ClipValue2Sql(oGet[17]:value)+","+;
                                 ClipValue2Sql(nTotalcIva)+","+;
                                 ClipValue2Sql(oGet[18]:value)+","+;
                                 ClipValue2Sql(nIvaPes)+","+;
                                 ClipValue2Sql(nCodIva)+","+;
                                 ClipValue2Sql(nDescuento)+","+;
                                 ClipValue2Sql(IF(nDescArt>0,nDescArt,nDescuGen))+","+;
                                 ClipValue2Sql(nSubTotal)+;
                       " )" )
ENDIF                                
oApp:oServer:NextResult()
oQryDet:Refresh()
oBrw:Refresh()
oBrw:MakeTotals()
cLetra := IF(nTipoDoc=5,"R",IF(oApp:tipo_iva<>6,IF(oGet[13]:nAt = 1 .or. oGet[13]:nAt = 2,"A","B"),"C"))
oGet[19]:cText := oBrw:aCols[7]:nTotal 
oGet[20]:cText := oBrw:aCols[8]:nTotal  
oGet[14]:cText:=1
oGet[14]:Refresh()
oGet[15]:cText:=SPACE(30)
oGet[25]:cText:=SPACE(30)
oGet[25]:Refresh()
oGet[15]:Refresh()
FOR i:= 16 TO 18
   oGet[i]:cText:=0
   oGet[i+10]:cText:=0
   oGet[i]:Refresh()
   oGet[i+10]:Refresh()
NEXT i
oGet[38]:cText := oBrw:aCols[7]:nTotal * (oGet[37]:value/100) * IF(nTipoDoc=3,-1,1)
oGet[21]:cText := oBrw:aCols[9]:nTotal + oGet[38]:value
oGet[19]:Refresh()
oGet[20]:Refresh()
oGet[21]:Refresh()
RETURN .t.

************************************************************************************
** Borrar item de la venta
STATIC FUNCTION EliminarItem()
LOCAL cLetra
oQryDet:DELETE()
oBrw:Refresh()
oQryDet:Refresh()
oBrw:Maketotals()
cLetra := IF(nTipoDoc=5,"R",IF(oApp:tipo_iva<>6,IF(oGet[13]:nAt = 1 .or. oGet[13]:nAt = 2,"A","B"),"C"))
oGet[19]:cText := oBrw:aCols[7]:nTotal 
oGet[20]:cText := oBrw:aCols[8]:nTotal   
oGet[38]:cText := oBrw:aCols[7]:nTotal * (oGet[37]:value/100) * IF(nTipoDoc=3,-1,1)
oGet[21]:cText := oBrw:aCols[9]:nTotal + oGet[38]:value
oGet[19]:Refresh()
oGet[20]:Refresh()
oGet[21]:Refresh()
RETURN .t.



************************************************************************************
STATIC FUNCTION ValidaCli(lVerCli)
LOCAL nNro:=0
DEFAULT lVerCli := .T.

IF lVerCli
  Buscar(oQryCli,oDlg,oGet[08])
  nLista:=oQryCli:lispre
  oGet[09]:cText := oQryCli:nombre
  oGet[10]:cText := oQryCli:direccion
  oGet[11]:ctext := oQryCli:localidad
  oGet[12]:cText := oQryCli:cuit
  oGet[22]:cText := oQryCli:dni
  oGet[31]:cText := oQryCli:descuento
  oGet[37]:cText := oQryCli:iibb  * IF(oApp:percep_iibb,1,0)
  oGet[32]:cText := oApp:oServer:Query("SELECT SUM(saldo) AS deuda FROM ge_"+oApp:cId+"ventas_cuota "+;
                                       "WHERE cliente = "+ClipValue2Sql(oQryCli:codigo)):deuda
  oGet[13]:Set(oQryCli:coniva)
  oGet[09]:Refresh()
  oGet[10]:Refresh()
  oGet[11]:Refresh()
  oGet[12]:Refresh()
  oGet[22]:Refresh()
  oGet[32]:Refresh()
  oGet[13]:Refresh()
  manticipo:=oQryCli:saldo
  IF oApp:oServer:Query("SELECT * FROM ge_"+oApp:cId+"remitos WHERE codcli = " + ClipValue2Sql(oQryCli:Codigo)):RecCount() > 0
      cRemitos:= "CLIENTE CON REMITOS PENDIENTES"
      oSay3:setcolor(CLR_RED)
  ELSE
      cRemitos:= "CLIENTE SIN REMITOS PENDIENTES"
      oSay3:setcolor(CLR_GREEN)
  ENDIF
ELSE
  cRemitos:= SPACE(20)
  nLista:=1
ENDIF
oSay3:Refresh()
cLetra := IF(nTipoDoc=5,"R",IF(oApp:tipo_iva<>6,IF(oGet[13]:nAt = 1 .or. oGet[13]:nAt = 2,"A","B"),"C"))
oGet[19]:cText := oBrw:aCols[7]:nTotal 
oGet[20]:cText := oBrw:aCols[8]:nTotal    
nNro:= oApp:oServer:Query("SELECT "+IF(cLetra="R","remito",IF(cLetra="A","facturaa","facturab"))+"+1 AS num FROM ge_"+oApp:cId+"punto WHERE ip = "+ClipValue2Sql(oApp:cip)):num
cNumComp := cLetra+STRTRAN(STR(nPuntoVta,4)+"-"+STR(nNro,8)," ","0")
oGet[6]:Refresh()
oSay1:SetText(cLetra) 
oGet[19]:Refresh()
oGet[20]:Refresh()
RETURN .T.


************************************************************************************
** Validar Articulo
STATIC FUNCTION ValidaArt()
/*
oGet[14] --> nCodArt   
oGet[15] --> cDetArt   
oGet[16] --> nCantidad 
oGet[17] --> nPunit    
oGet[18] --> nPtotal   
*/
oQryArt:= oApp:oServer:Query("SELECT codigo,nombre,stockact,precioven,iva,fecmod,reventa,preciocos,endolares,marca "+;
                             "FROM ge_"+oApp:cId+"articu WHERE codigo = "+ClipValue2Sql(oGet[14]:cText))
IF oQryArt:RecCount() = 0 .OR. oGet[14]:value = 0
  oGet[14]:cText:= 0
  BuscarArt(oQryArt,oDlg,oGet[14])
ENDIF
IF oApp:usar_dias_precio
  IF oQryArt:fecmod < (DATE()- oApp:dias_precio)
    MsgStop("El precio del articulo seleccionado esta desactualizado "+CHR(10)+;
            "Verifique la fecha de modificacion","Atencion!")
    RETURN .f.
  ENDIF
ENDIF
oGet[15]:cText := oQryArt:nombre
oGet[16]:cText := 1
oGet[17]:ctext := IF(nTipoDoc<6,IF(nLista=1,oQryArt:precioven,oQryArt:reventa),oQryArt:preciocos) * IF(oQryArt:endolares,oQryPar:dolar,1)
oGet[18]:cText := IF(nTipoDoc<6,IF(nLista=1,oQryArt:precioven,oQryArt:reventa),oQryArt:preciocos) * IF(oQryArt:endolares,oQryPar:dolar,1)

     
oGet[14]:Refresh()
oGet[15]:Refresh()
oGet[16]:Refresh()
oGet[17]:Refresh()
oGet[18]:Refresh()
RETURN .t.

************************************************************************************
** Cerrar el archivo abierto
STATIC FUNCTION cerrar ( oWnd )
LOCAL aNueva := {}, i, j
  oQryCli:End()
  oQryCli := NIL
  oQryPar:End()
  oQryPar:= nil
  oQryDet:End()
  oQryDet:= nil

  j := ASCAN(oApp:aVentanas,cVentana)
  FOR i := 1 TO LEN(oApp:aVentanas)
      IF i <> j
         AADD(aNueva,oApp:aVentanas[i])
      ENDIF
  NEXT i
  oApp:aVentanas := ACLONE(aNueva)
RETURN .t.

************************************************************************************
** Validaciones a hacer cuando quiere grabar (Incluye forma de pago y facturacion)
STATIC FUNCTION Validaciones()
LOCAL oGet1 := ARRAY(20), oBot := ARRAY(4), oForm, lRta := .f., oBrw2, oQryCuo, dFecVto,;
      nCodCue:=0,nNumOpe:=0,nNumDep,oBrw1,nCliente,nNetoPar,;
      cDetTar, cDetChe := SPACE(30), aTablaIva := {}, oTabIva, nNro:=0, cTipoDoc,nCodAut:=0,nCodVen:=0,;
      oQryVen, oQryPag, oQryPagCon, oQryPagFac, base, oErr, nRecibo, nTipFor, nSaldo := 0,cTipo:=" ",lCuenta:=.f.,;
      oQryTar, aTarjetas, oError,oDlg2, nCodPro:=0,nCodBan:=0,cObserva1:=SPACE(255), lFisc,cCae,dFecVtoC
cLetra := IF(nTipoDoc=5,"R",IF(oApp:tipo_iva<>6,IF(oGet[13]:nAt = 1 .or. oGet[13]:nAt = 2,"A","B"),"C"))
// Validar que el DATOS CLIENTE
IF (oGet[13]:nAt <> 5 .and. (EMPTY(oGet[12]:cText) .OR. oGet[12]:cText = "  -        - ")) .and. nTipoDoc <> 6
   MsgStop("C.U.I.T. Invalido para tipo de condicion de I.V.A.","Error")
   RETURN nil
ENDIF

DO CASE 
CASE nTipoDoc < 4
// Validacion datos Ok
//Traigo trarjetas para el detalle de la tarjeta
oQryTar:= oApp:oServer:Query("SELECT * FROM ge_"+oApp:cId+"tarjetas")
aTarjetas:={}
aTarjetas:= oQryTar:FillArray(,{"nombre"})
cDetTar:= aTarjetas[1]

oApp:oServer:Execute("CREATE TEMPORARY TABLE IF NOT EXISTS `transi_cheque` ("+;
                           "`tipo` int(1) NOT NULL DEFAULT 1,"+;
                           "`NUMBAN` int(6) NOT NULL DEFAULT 1,"+;
                           "`NUMCHE` int(10) NOT NULL DEFAULT 0,"+;
                           "`FECEMI` date DEFAULT NULL,"+;
                           "`FECVTO` date DEFAULT NULL,"+;
                           "`IMPORTE` decimal(10,2) DEFAULT 0,"+;
                           "`ORDEN` int(8) DEFAULT 0,"+;
                           "`CODPRO` int(8) DEFAULT 0, "+;
                           "`ESTADO` varchar(1) DEFAULT ' ',"+;
                           "`CODCLI` int(8) DEFAULT 0,"+;
                           "`RECIBO` int(8) DEFAULT 0,"+;
                           "PRIMARY KEY (tipo,numban,numche) ) ENGINE=INNODB DEFAULT CHARSET=latin1")
oApp:oServer:NextResult()
oApp:oServer:Execute("TRUNCATE transi_cheque")
oApp:oServer:NextResult()      
oQryChe := oApp:oServer:Query("SELECT * FROM transi_cheque")

oApp:oServer:Execute("";
    + "CREATE TEMPORARY TABLE IF NOT EXISTS `ventas_cuota_h` ( ";
    + "  `CUOTA` int(3) DEFAULT '0',";
    + "  `FECVTO` date DEFAULT NULL,";
    + "  `IMPORTE` decimal(10,2) DEFAULT NULL";
    + " ) ENGINE=InnoDB DEFAULT CHARSET=utf8")
  
oApp:oServer:NextResult()
oQryCuo := oApp:oServer:Query("SELECT * FROM ventas_cuota_h  ")      
oQryCuo:ZAP()

nEfecti  := 0
nTarjet  := 0
nTransfe := 0
nCheque  := 0
nCtaCte  := 0
nCuotas  := 1 
dFecVto  := DATE()+30
mpagado  := nTotal 


DO WHILE .T.
DEFINE DIALOG oDlg2 RESOURCE "FORMAPAG" OF oDlg
    oDlg2:lHelpIcon := .f.
    REDEFINE GET oGet1[1] VAR mpagado   ID 110 OF oDlg2 PICTURE "9999999.99" WHEN(.F.)
    REDEFINE GET oGet1[2] VAR manticipo ID 111 OF oDlg2 PICTURE "9999999.99" WHEN(.f.)
    REDEFINE GET oGet1[3] VAR nEfecti   ID 112 OF oDlg2 PICTURE "9999999.99"
    REDEFINE GET oGet1[4] VAR nTarjet   ID 113 OF oDlg2 PICTURE "9999999.99"
    REDEFINE GET oGet1[5] VAR nTransfe  ID 4001 OF oDlg2 PICTURE "99999999.99"
    REDEFINE GET oGet1[7] VAR nCheque   ID 114 OF oDlg2 PICTURE "9999999.99" WHEN(.F.)
    REDEFINE GET oGet1[6] VAR nCtaCte   ID 115 OF oDlg2 PICTURE "9999999.99";
                    WHEN((oGet1[6]:cText := mpagado - (manticipo+nEfecti +;
                                      nTarjet + nTransfe + nCheque)) = "-9999999")
    //cant cuotas
    REDEFINE GET oGet1[09] VAR nCuotas  ID 118  OF oDlg2  PICTURE "9999" WHEN (nCtaCte > 0 .and. oApp:usar_cuotas)
    //vto primera cuota
    REDEFINE GET oGet1[10] VAR dFecVto  ID 119  OF oDlg2 WHEN (nCtaCte > 0)
    //GRILLA ----------------------------------------------------
    REDEFINE XBROWSE oBrw2 DATASOURCE oQryCuo;
       COLUMNS "CUOTA","FECVTO","IMPORTE";
       HEADERS "Cuota","Fecha","Importe";
       FOOTERS ;
       SIZES 80,100,170;
    ID 4003 OF oDlg2 
    PintaBrw(oBrw2,0)
    oBrw2:aCols[2]:lAutoSave := .t.
    oBrw2:aCols[2]:nEditType := EDIT_GET 
    oBrw2:nMarqueeStyle := 6
    oBrw2:aCols[3]:nFooterTypE := AGGR_SUM
    oBrw2:MakeTotals()

    REDEFINE BUTTON oBot[1] ID 116 OF oDlg2 ACTION ;
               (AddCheqT(oBot[1]),oBrw1:Refresh(),oBrw1:MakeTotals(),;
                                oGet1[7]:cText := oBrw1:aCols[6]:nTotal,oGet1[7]:Refresh())
    REDEFINE XBROWSE oBrw1  ID 120  OF oDlg2 DATASOURCE oQryChe ;
              COLUMNS "tipo","numban","numche","fecemi","fecvto","importe";
              HEADERS "Tipo","Banco","Numero","Emision","Vto.","Importe" FOOTERS;
              SIZES 30,45,55,70,70,75
    oBrw1:bKeyDown := { | nKey, nFlags |;
       IF(nKey==46,IF(MsgNoYes("Borra el cheque?"),;
                     (oQryChe:Delete(),oBrw1:Refresh(),oBrw1:MakeTotals(),;
                      oGet1[7]:cText := oBrw1:aCols[6]:nTotal, oGet1[7]:Refresh(), oGet1[3]:SetFocus()),;
                     .f.),.f.)}      
    oBrw1:aCols[6]:nFooterType := AGGR_SUM
    PintaBrw(oBrw1,0)     

    //arma plan 
    REDEFINE BUTTON oBot[4]  ID 201 OF oForm ACTION ArmaPLan(oGet1, oBrw2, oQryCuo );
             WHEN(IF(nTipoCli=1,ROUND(nCtaCte,2) > 0 .and. oApp:usar_cuotas,ROUND(nCtaCte=0,2)))   
    REDEFINE BUTTON oBot[2]  ID 102 OF oDlg2 ACTION (lrta:=.t.,oDlg2:End()) 
    REDEFINE BUTTON oBot[3]  ID 103 OF oDlg2 ACTION (lRtA:=.F.,oDlg2:End())
ACTIVATE DIALOG oDlg2 CENTER

IF !lRta
   RETURN nil
ENDIF 
nCliente:= IF(nTipoCli=1,oGet[8]:value,-1)

IF nCtaCte > 0 .and. nCliente = 1
   MsgStop("Consumidor final no puede pagar en Cta.Cte.","Error")
   LOOP
ENDIF  

IF oApp:usar_limite_cred
  IF nCtaCte > 0 .AND. ((oQryCli:saldo + nCtaCte) > oQryCli:limite) .and. nCliente > 0
     IF !PedirClave("El cliente que quiere facturar tiene el limite de credito excedido, ingrese la clave de autorizacion para continuar",oDlg)
        LOOP
     ENDIF
  ENDIF
ENDIF

IF nCliente = -1 .and. nCtaCte > 0 
   MsgStop("Un cliente ocacional no puede pagar en Cta. Cte.","Error")
   LOOP
ENDIF

IF oApp:usar_dias_deuda
    IF oApp:oServer:Query("SELECT DATEDIFF(CURDATE(),MIN(fecvto)) AS dias FROM ge_"+oApp:cId+"ventas_cuota "+;
                          "WHERE saldo > 0 AND cliente = "+ClipValue2Sql(oGet[08]:cText)):dias > oApp:dias_deuda
       IF !PedirClave("El cliente que queire facturar tiene una deuda impaga que excede el limite de dias permitidos a deber,"+;
                      " ingrese la clave de autorizacion para continuar",oDlg)
          LOOP
       ENDIF
    ENDIF
ENDIF


IF nTransfe > 0
     lCuenta:=  DatosTransferencia(@nCodCue,@nNumOpe,@cObserva1)
     IF !lCuenta
        LOOP
     ENDIF
ENDIF  
EXIT 
ENDDO

oTabIva := oApp:oServer:Query("SELECT codiva, SUM(stotal) AS neto,SUM(iva) AS iva FROM ventas_det_H GROUP BY codiva")
DO WHILE !oTabIva:Eof()
   AADD(aTablaIva,{oTabIva:codiva,oTabIva:neto,oTabIva:iva})
   oTabIva:Skip()
ENDDO
IF nTipoDoc<=3
   dFecVtoC:= DATE()
   cCae:= ""
   nTipFor:=01
   lFisc:= MsgYesNo("¿Desea emitir el tiquet fiscal?","Atencion!")
   IF lFisc
      IF oQryPun:tipofac = 1
          FacturaElec(oGet, nTipoFac, nPuntoVta, nTipoDoc, cLetra, aTablaIva, oBrw, @nNro, @cCae, @dFecVtoC,@nTipFor)
      ELSE
          nNro := FacturaFiscal(oQryDet,oGet[08]:Value)
          IF nNro = 0
             MsgStop("Fallo el impresor fiscal","Error")
             Return nil 
          ENDIF 
      ENDIF     
   ELSE
     nNro := oApp:oServer:Query("SELECT presupu FROM ge_"+oApp:cId+"punto WHERE ip = "+ ClipValue2Sql(oApp:cip)):presupu+1
     cLetra := "X"
     cNumComp := cLetra+STRTRAN(STR(nPuntoVta,4)+"-"+STR(nNro,8)," ","0")
     cTipoDoc := IF(nTipoDoc=1,"FC",IF(nTipoDoc=2,"ND","NC"))
     *FacturaNoFiscal(oQryDet,oGet[08]:Value,cTipoDoc+cNumComp)
   ENDIF
   // Falta las impresiones fiscales de NC Y ND y Presupuesto y Remito  
ENDIF
cNumComp := cLetra+STRTRAN(STR(nPuntoVta,4)+"-"+STR(nNro,8)," ","0")
cTipoDoc := IF(nTipoDoc=1,"FC",IF(nTipoDoc=2,"ND","NC"))
  oQryPag    := oApp:oServer:Query( "SELECT * FROM ge_"+oApp:cId+"pagos LIMIT 0")
  oQryPagFac := oApp:oServer:Query( "SELECT * FROM ge_"+oApp:cId+"pagfac LIMIT 0")
  oQryPagCon := oApp:oServer:Query( "SELECT * FROM ge_"+oApp:cId+"pagcon LIMIT 0")
  oQryVen    := oApp:oServer:Query( "SELECT * FROM ge_"+oApp:cId+"ventas_encab LIMIT 0")
  oQryCuo    := oApp:oServer:Query( "SELECT * FROM ge_"+oApp:cId+"ventas_cuota LIMIT 0")
  // Grabar la factura
  // Grabo el pago

  TRY 

 
 oApp:oServer:BeginTransaction()
      IF nEfecti + nTarjet + nCheque + nTransfe + manticipo  > 0
          // Grabo el pago si no es todo Cuenta Corriente
          //1 - GRABACION DE PAGO -------------------------------------------------------------------
          nRecibo :=  oApp:oServer:GetAutoIncrement("ge_"+oApp:cId+"pagos")

           // Grabo el registro de PAGOS
          base := oQryPag:GetBlankRow()
          base:numero  := nRecibo
          base:cliente := nCliente
          base:total   := (nEfecti + nTarjet + nTransfe + nCheque) * IF(cTipoDoc="NC",-1,1) 
          base:fecha   := oGet[1]:value
          base:usuario := oApp:usuario
          base:fecmod  := DATE()
          base:ip      := oApp:cIp
          base:vendedor:= cVendedor
          base:interes := 0
          oQryPag:oRow := base
          oQryPag:Save()

          //Grabo Pago Factura
          base := oQryPagFac:GetBlankRow()
          base:numero := nRecibo
          base:ticomp:= LEFT(cTipoDoc,2)
          base:letra := LEFT(cNumComp,1)
          base:numcomp:= RIGHT(cNumComp,13)
          base:fecha:= oGet[1]:value
          base:importe := IF(nEfecti + nTransfe + nTarjet + nCheque + manticipo > nTotal,nTotal,;
                            nEfecti + nTransfe + nTarjet + nCheque + manticipo) * IF(cTipoDoc="NC",-1,1)
          oQryPagFac:oRow := base
          oQryPagFac:Save()
        
          
          // Grabo los conceptos pagados
          IF nEfecti > 0
                  base := oQryPagCon:GetBlankRow()
                  base:numero  := nRecibo
                  base:importe := nEfecti * IF(cTipoDoc="NC",-1,1)
                  base:observa := "EFECTIVO"
                  base:codcon := 1
                  oQryPagCon:oRow := base
                  oQryPagCon:Save()
          ENDIF   

           // INGRESO CONCEPTO TRANSFERENCIA
           IF nTransfe > 0
              oApp:oServer:Execute("INSERT INTO ge_"+oApp:cId+"deposito " + ;
                              " (banco,tipo,fecemi,fecacr,numope,detalle,usuario,fecmod,ip,importe) VALUES "+;
                              " ("+;
                                ClipValue2SQL(nCodCue)+","+;
                                "'D',"+;
                                ClipValue2SQL(DATE())+","+;
                                  ClipValue2SQL(DATE())+","+;
                                  ClipValue2Sql(nNumOpe)+","+;
                                  ClipValue2SQL(cObserva1)+","+;
                                  ClipValue2SQL(oApp:usuario)+","+;
                                  ClipValue2SQL(DATE())+","+;
                                  ClipValue2SQL(oApp:cIp)+","+;
                                  ClipValue2SQL(nTransfe)+")")
              nNumDep:= oApp:oServer:Query("SELECT MAX(id) AS numero FROM ge_"+oApp:cId+"deposito"):numero 
              oApp:oServer:Execute("UPDATE ge_"+oApp:cId+"pagos SET iddepo = "+ ClipValue2Sql(nNumDep) + " WHERE numero = "+ClipValue2Sql(nRecibo))
              oApp:oServer:Execute("INSERT INTO ge_"+oApp:cId+"pagcon SET numero  = " + ClipValue2Sql(nRecibo)+","+;
                                                     "codcon  = 2, "+;
                                                     "importe = "+ ClipValue2Sql(nTransfe)+","+;
                                                     "observa = 'TRANSFERENCIA'")
           ENDIF
          
          IF ncheque > 0
             base := oQryPagCon:GetBlankRow()
             base:numero  := nRecibo
             base:importe := nCheque * IF(cTipoDoc="NC",-1,1)
             base:observa := IF(EMPTY(cDetChe),"CHEQUE: " + STR(nRecibo),cDetChe)
             base:codcon := 3
             oQryPagCon:oRow := base
             oQryPagCon:Save()
          ENDIF   

          IF nTarjet > 0
             base := oQryPagCon:GetBlankRow()
             base:numero  := nRecibo
             base:importe := nTarjet * IF(cTipoDoc="NC",-1,1)
             base:codcon := 4
             base:observa := IF(EMPTY(cDetTar),"TARJETA: " + STR(nRecibo),cDetTar)
             oQryPagCon:oRow := base
             oQryPagCon:Save()
          ENDIF   
          IF manticipo > 0
          oApp:oServer:Execute("INSERT INTO ge_"+oApp:cId+"pagcon SET numero  = " + ClipValue2Sql(nRecibo)+","+;
                                               "codcon  = 7, "+;
                                               "importe = "+ ClipValue2Sql(manticipo * IF(cTipoDoc="NC",-1,1))+","+;
                                               "observa = 'ANTICIPO'")
          ENDIF

          oApp:oServer:Execute("INSERT INTO ge_"+oApp:cId+"ventas_cuota (tipo,letra,numero,cuota,cantcuo,fecha,cliente,neto,importe,saldo,estado,fecvto,"+;
                                                        "nombre,cuit,dni,direccion,localidad,usuario,vendedor,fecmod,ip) "+;
                              "VALUES ("+ClipValue2Sql(cTipoDoc)+","+;
                                         ClipValue2Sql(LEFT(cNumComp,1))+","+;
                                         ClipValue2Sql(RIGHT(cNumComp,13))+","+;
                                         "0," + ClipValue2Sql(oGet1[9]:value) +","+;
                                         "CURDATE(),"+ ClipValue2Sql(nCliente)+",0,"+;
                                         ClipValue2Sql(nEfecti + nTarjet + nCheque + nTransfe + manticipo)+","+;
                                         "0,'P',CURDATE(),"+;
                                         ClipValue2Sql(oGet[9]:cText)+","+;
                                         ClipValue2Sql(oGet[12]:cText)+","+;
                                         ClipValue2Sql(oGet[22]:cText)+","+;
                                         ClipValue2Sql(oGet[10]:cText)+","+;
                                         ClipValue2Sql(oGet[11]:cText)+","+;
                                         ClipValue2SQL(oApp:usuario)+","+;
                                         ClipValue2SQL(cVendedor)+","+;
                                         ClipValue2SQL(DATE())+","+;
                                         ClipValue2SQL(oApp:cIp)+")")
      ENDIF  
      nNetoPar:= IF(nCtaCte<oBrw:aCols[7]:nTotal,nCtaCte,oBrw:aCols[7]:nTotal)
      //2 - CUOTAS DE PAGO EN CTA CTE ------------------------------------------------------      
         oApp:oServer:Execute("INSERT INTO ge_"+oApp:cId+"ventas_cuota (tipo,letra,numero,cuota,cantcuo,fecha,cliente,neto,importe,saldo,estado,fecvto,"+;
                                                        "nombre,cuit,dni,direccion,localidad,usuario,vendedor,fecmod,ip) "+;
                              "(SELECT "+ClipValue2Sql(cTipoDoc)+","+;
                                         ClipValue2Sql(LEFT(cNumComp,1))+","+;
                                         ClipValue2Sql(RIGHT(cNumComp,13))+","+;
                                         "cuota," + ClipValue2Sql(oGet1[9]:value) +","+;
                                         "CURDATE(),"+ ClipValue2Sql(nCliente)+","+ClipValue2Sql(nNetoPar/oGet1[9]:value)+","+;
                                         "importe,"+IF(nCtaCte>=0,"importe","0")+","+IF(nCtaCte=0,"'P'","'I'")+",fecvto, "+;
                                         ClipValue2Sql(oGet[9]:cText)+","+;
                                         ClipValue2Sql(oGet[12]:cText)+","+;
                                         ClipValue2Sql(oGet[22]:cText)+","+;
                                         ClipValue2Sql(oGet[10]:cText)+","+;
                                         ClipValue2Sql(oGet[11]:cText)+","+;
                                         ClipValue2SQL(oApp:usuario)+","+;
                                         ClipValue2SQL(cVendedor)+","+;
                                         ClipValue2SQL(DATE())+","+;
                                         ClipValue2SQL(oApp:cIp)+" "+;
                              " FROM ventas_cuota_h)")
      //3 - FACTURA ------------------------------------------------------------------------
      IF nCtaCte <= 0  
         oApp:oServer:Execute("UPDATE ge_"+oApp:cId+"clientes SET saldo = "+ClipValue2Sql(ABS(nCtaCte)) + " WHERE codigo = " +ClipValue2Sql(nCliente))
      ELSE
         oApp:oServer:Execute("UPDATE ge_"+oApp:cId+"clientes SET saldo = 0 WHERE codigo = " +ClipValue2Sql(nCliente))
         IF !oApp:usar_cuotas
            oApp:oServer:Execute("INSERT INTO ge_"+oApp:cId+"ventas_cuota (tipo,letra,numero,cuota,cantcuo,fecha,cliente,neto,importe,saldo,estado,fecvto,"+;
                                                        "nombre,cuit,dni,direccion,localidad,usuario,vendedor,fecmod,ip) "+;
                              "VALUES ("+ClipValue2Sql(cTipoDoc)+","+;
                                         ClipValue2Sql(LEFT(cNumComp,1))+","+;
                                         ClipValue2Sql(RIGHT(cNumComp,13))+","+;
                                         "1,1,"+;
                                         "CURDATE(),"+ ClipValue2Sql(nCliente)+","+ClipValue2Sql(nNetoPar)+","+;
                                         ClipValue2Sql(nCtaCte)+","+ClipValue2Sql(nCtaCte)+","+;
                                         "'I',CURDATE(),"+;
                                         ClipValue2Sql(oGet[9]:cText)+","+;
                                         ClipValue2Sql(oGet[12]:cText)+","+;
                                         ClipValue2Sql(oGet[22]:cText)+","+;
                                         ClipValue2Sql(oGet[10]:cText)+","+;
                                         ClipValue2Sql(oGet[11]:cText)+","+;
                                         ClipValue2SQL(oApp:usuario)+","+;
                                         ClipValue2SQL(cVendedor)+","+;
                                         ClipValue2SQL(DATE())+","+;
                                         ClipValue2SQL(oApp:cIp)+")")
         ENDIF
      ENDIF

      DO WHILE !oQryChe:Eof()
      oApp:oServer:Execute("INSERT INTO ge_"+oApp:cId+"cheter SET numban  = "+ClipValue2Sql(oQryChe:numban)+","+;
                                                            "numche  = "+ClipValue2Sql(oQryChe:numche)+","+;
                                                            "fecing  = "+ClipValue2Sql(oQryChe:fecemi)+","+;
                                                            "fecvto  = "+ClipValue2Sql(oQryChe:fecvto)+","+;
                                                            "importe = "+ClipValue2Sql(oQryChe:importe)+","+;
                                                            "recibo  = "+ClipValue2Sql(nRecibo)+","+;
                                                            "codcli  = "+ClipValue2Sql(nCliente)+","+;
                                                            "noorden = 0,"+;
                                                            "demo    = 0,"+;
                                                            "usuario = "+ClipValue2Sql(oApp:usuario)+","+;
                                                            "fecmod  = CURDATE(),"+;
                                                            "ip      = "+ClipValue2Sql(oApp:cIp)+","+;
                                                            "estado  = 'C'")
         oQryChe:Skip()
     ENDDO   
      //-- BORRO PEDIDO SI LO USO
      IF lUsoPedido
         oApp:oServer:Execute("DELETE FROM ge_"+oApp:cId+"pedidos WHERE codcli = "+ ClipValue2Sql(oGet[8]:cText))
      ENDIF
      //-- DETALLE DE IVA DE LA VENTA
      oApp:oServer:Execute("INSERT INTO ge_"+oApp:cId+"ventivadet (tipocomp,letra,numfac,codiva,neto,iva) "+;
                           "(SELECT "+ClipValue2Sql(cTipoDoc)+","+ClipValue2Sql(LEFT(cNumComp,1))+","+ClipValue2Sql(RIGHT(cNumComp,13))+;
                            ", codiva, SUM(stotal) AS neto,SUM(iva) AS iva FROM ventas_det_H GROUP BY codiva)")
      //-- detalle de venta
      oApp:oServer:Execute("INSERT INTO ge_"+oApp:cId+"ventas_det "+;
                        " (codart,detart,cantidad,punit,fecha,codcli,nrofac,importe,neto,iva,codiva,neton,descu,pcosto) "+;
                        " (SELECT codart,detart,cantidad,punit,"+ClipValue2Sql(oGet[1]:value)+","+ClipValue2Sql(nCliente)+","+;
                        "         "+ ClipValue2Sql(cTipoDoc+cNumComp)+",ptotal, stotal,iva,codiva,neto,descuento,pcosto FROM ventas_det_H)")
      //-- encabezado de venta
      oApp:oServer:Execute("INSERT INTO ge_"+oApp:cId+"ventas_encab (ticomp,letra,numcomp,codcli,coniva,fecha,neto,iva,iibb,importe,tipopag,observa,"+;
                                                     "nombre,cuit,dni,direccion,localidad,percep,cae,fecvto,tipfor,usuario,fecmod,ip,vendedor) VALUES "+;
                           "("+ClipValue2Sql(cTipoDoc)+","+ClipValue2Sql(LEFT(cNumComp,1))+","+ClipValue2Sql(RIGHT(cNumComp,13))+","+;
                            ClipValue2Sql(nCliente)+","+ClipValue2Sql(nConIva)+","+;
                            ClipValue2Sql(DATE())+","+ClipValue2Sql(oBrw:aCols[7]:nTotal)+","+ClipValue2Sql(oBrw:aCols[8]:nTotal)+","+;
                            ClipValue2SQL(oGet[38]:cText)+","+ClipValue2Sql(nTotal)+",1,"+ClipValue2Sql(oGet[23]:cText)+","+;
                            ClipValue2Sql(oGet[9]:cText)+","+ClipValue2Sql(oGet[12]:cText)+","+ClipValue2Sql(oGet[22]:cText)+","+;
                            ClipValue2Sql(oGet[10]:cText)+","+ClipValue2Sql(oGet[11]:cText)+","+ClipValue2SQL(oGet[37]:cText)+","+;
                            ClipValue2Sql(cCae)+","+ClipValue2Sql(dFecVtoC)+","+ClipValue2Sql(STRTRAN(STR(nTipFor,2)," ","0"))+","+;
                            ClipValue2Sql(oApp:usuario)+","+ClipValue2SQL(DATE())+","+ClipValue2SQL(oApp:cIp)+","+ClipValue2Sql(cVendedor)+")")        
      
      IF oApp:usar_puntos
         oApp:oServer:Execute("UPDATE ge_"+oApp:cId+"clientes SET puntos = puntos + "+ClipValue2Sql(INT(nTotal/oApp:pesos_x_punto)))
      ENDIF

      //ACTUALIZO EL STOCK DE LOS ARTICULOS VENDIDOS
      oApp:oServer:Execute("UPDATE ventas_det_H v LEFT JOIN ge_"+oApp:cId+"articu a ON a.codigo = v.codart "+;
                           "SET a.stockact = a.stockact - IF("+ClipValue2Sql(cTipoDoc)+"='NC',v.cantidad*(-1),v.cantidad)")

      //BORRO FACTURAS PENDIENTES USADAS (SI NO USA NINGUNA ES 0)
      oApp:oServer:Execute("DELETE FROM ge_"+oApp:cId+"ventas_encab_p WHERE id = "+ClipValue2Sql(nBorrarPend))
      oApp:oServer:Execute("DELETE FROM ge_"+oApp:cId+"ventas_det_p   WHERE numven = "+ClipValue2Sql(nBorrarPend))

      IF lFisc       
         IF cLetra = "A"
          oApp:oServer:Execute("UPDATE ge_"+oApp:cId+"punto SET facturaa = "+ClipValue2Sql(nNro)+" WHERE ip = "+ ClipValue2Sql(oApp:cip))
         ELSE  
          oApp:oServer:Execute("UPDATE ge_"+oApp:cId+"punto SET facturab = "+ClipValue2Sql(nNro)+" WHERE ip = "+ ClipValue2Sql(oApp:cip))          
         ENDIF 
      ELSE
         oApp:oServer:Execute("UPDATE ge_"+oApp:cId+"punto SET presupu = presupu+1 WHERE ip = "+ ClipValue2Sql(oApp:cip))
      ENDIF         
      oApp:oServer:CommitTransaction()
      IF lFisc
	  	 PrintFactuElec(cTipoDoc,cNumComp)  
	  ELSE 
	  	 FacturaNoFiscal(oQryDet,oGet[08]:Value,cTipoDoc+cNumComp)
	  ENDIF
  CATCH oErr
      MsgStop("Error al grabar"+CHR(10)+oErr:description,"Error")
      oApp:oServer:RollBack()
      RETURN .F.
  END TRY
 /*
  IF nEfecti+nTarjet+nCheque+manticipo > 0  
     lRta := MsgYesNo("Desea imprimir el recibo de anticipos?","Atencion")
     IF lRta 
        Reci(nRecibo,.f.)
     ENDIF
  ENDIF   
*/
CASE nTipoDoc = 4
     lFisc := MsgNoYes("Emitir el Presupuesto en u$s?","Atencion")
     PrintPresupu(oQryDet,oGet,oBrw,lFisc)

CASE nTipoDoc = 5
  nCodAut:=0
  cTipoDoc:="RE"
  cNumComp:= STRTRAN(STR(nPuntoVta,4)+"-"+STR(oApp:oServer:Query("SELECT remito FROM ge_"+oApp:cId+"punto WHERE ip = "+ ClipValue2Sql(oApp:cip)):remito+1,8)," ","0")  
  TRY
  oApp:oServer:BeginTransaction()                     
  oApp:oServer:Execute("INSERT INTO ge_"+oApp:cId+"remitos "+;
                        " (codart,detart,cantidad,fecha,codcli,nrorem,codiva,facturado,incrp,importe,iva,neto,neton,tipo,auto,vendedor) "+;
                        " (SELECT codart,detart,cantidad,"+ClipValue2Sql(oGet[1]:value)+","+ClipValue2Sql(oGet[8]:value)+","+;
                        + ClipValue2Sql(cTipoDoc+cNumComp)+",codiva,FALSE,descup,0,0,0,0, "+;
                        "'I',"+ClipValue2Sql(nCodAut)+","+ClipValue2Sql(cVendedor)+" "+;
                        "   FROM ventas_det_H WHERE codart > 0)")
  oApp:oServer:Execute("INSERT INTO ge_"+oApp:cId+"remitos "+;
                        " (codart,detart,cantidad,fecha,codcli,nrorem,codiva,facturado,incrp,importe,iva,neto,neton,tipo,auto,vendedor) "+;
                        " (SELECT codart,detart,cantidad,"+ClipValue2Sql(oGet[1]:value)+","+ClipValue2Sql(oGet[8]:value)+","+;
                        + ClipValue2Sql(cTipoDoc+cNumComp)+",codiva,FALSE,descup,ptotal,iva,stotal,neto, "+;
                        "'I',"+ClipValue2Sql(nCodAut)+","+ClipValue2Sql(cVendedor)+" "+;
                        "   FROM ventas_det_H WHERE codart = 0)")
  //ACTUALIZO EL STOCK DE LOS ARTICULOS VENDIDOS
  oApp:oServer:Execute(" UPDATE ventas_det_H v LEFT JOIN ge_"+oApp:cId+"articu a ON a.codigo = v.codart SET a.stockact = a.stockact - v.cantidad")

  oApp:oServer:Execute("UPDATE ge_"+oApp:cId+"punto SET remito = remito + 1 WHERE ip = "+ ClipValue2Sql(oApp:cip))
  oApp:oServer:CommitTransaction()
  PrintRemito(oQryDet,oGet,oBrw,cNumComp)
CATCH oError
   ValidaError(oError)
END TRY
ENDCASE 
LimpiarForm()
RETURN nil


***********************************************************************************************************************************
***** SI ES UNA TRANSFERENCIA PIDO LOS DATOS DE LA CUENTA 
STATIC FUNCTION DatosTransferencia(nCodCue,nNumOpe,cObserva)
LOCAL oGet := ARRAY(4), oBot := ARRAY(3), oForm, lRta := .F., aCor, base, oError, oQry,;
      cNomCue:=SPACE(50),oQryCue,oDlg1

oQryCue:= oApp:oServer:Query("SELECT * FROM ge_"+oApp:cId+"cuentas")


DEFINE DIALOG oForm TITLE "Cuenta de la transferencia";
       FROM 05,15 TO 15,85 OF oDlg FONT oApp:oFont
   
   @ 07, 05 SAY "Cuenta:"            OF oForm PIXEL SIZE 40,12 RIGHT
   @ 22, 05 SAY "Operacion:"         OF oForm PIXEL SIZE 40,12 RIGHT
   @ 37, 05 SAY "Observaciones:"     OF oForm PIXEL SIZE 40,12 RIGHT
  
   @ 05, 50 GET oGet[1] VAR nCodCue OF oForm SIZE 25,12 RIGHT PICTURE "99" PIXEL;
                VALID(Buscar(oQryCue,oDlg1,oGet[1],oGet[2]));
                ACTION (oGet[1]:cText:= 0, Buscar(oQryCue,oDlg1,oGet[1],oGet[2])) BITMAP "BUSC1"
   @ 05, 80 GET oGet[2] VAR cNomCue PICTURE "@!" OF oForm PIXEL WHEN(.F.)
   @ 20, 50 GET oGet[3] VAR nNumOpe PICTURE "99999999" OF oForm PIXEL RIGHT 
   @ 35, 50 GET oGet[4] VAR cObserva PICTURE "@!" OF oForm PIXEL SIZE 200,12
   
   acor := AcepCanc(oForm)
   @ acor[1],acor[2] BUTTON oBot[1] PROMPT "&Grabar" OF oForm SIZE 30,10 ;
           ACTION ((lRta := .t.), oForm:End() ) PIXEL
   @ acor[3],acor[4] BUTTON oBot[2] PROMPT "&Cancelar" OF oForm SIZE 30,10 ;
           ACTION ((lRta := .f.), oForm:End() ) PIXEL CANCEL
ACTIVATE DIALOG oForm CENTER ON INIT oGet[1]:SetFocus()

IF !lRta
   RETURN .F.
ENDIF

RETURN .T.

*********************************************************************************************************************************
************* SELECCIONA EL TIPO DE REMITO (INTERNO O EXTERNO)
STATIC FUNCTION TipoDeRemito(nCodAut,nCodVen,cTipo)
LOCAL oDlgR,oGet1:=ARRAY(4),oRadio,cNomAut:=SPACE(30),cNomVen:=SPACE(30),;
      oQryVend,oQryAut,lRta:=.f.,oBot1:=ARRAY(2),nTipo:=2
oQryVend:= oApp:oServer:Query("SELECT * FROM ge_"+oApp:cId+"vendedores")
oQryAut:= oApp:oServer:Query("SELECT * FROM ge_"+oApp:cId+"auto")
nCodAut:=0
nCodVen:=0
DEFINE DIALOG oDlgR RESOURCE "REMITO" OF oDlg
oDlgR:lHelpIcon := .f.

 REDEFINE RADIO oRadio VAR nTipo ID 100,200 OF oDlgR
 REDEFINE GET oGet1[1] VAR nCodAut ID 101 OF oDlgR PICTURE "999999" WHEN(nTipo=1);
              VALID(Buscar(oQryAut,oDlgR,oGet1[1],oGet1[2]));
              ACTION (oGet1[1]:cText:= 0, Buscar(oQryAut,oDlgR,oGet1[1],oGet1[2])) BITMAP "BUSC1" 
 REDEFINE GET oGet1[2] VAR cNomAut ID 102 OF oDlgR PICTURE "@!" WHEN(.F.)
 REDEFINE GET oGet1[3] VAR nCodVen ID 103 OF oDlgR PICTURE "99" WHEN(nTipo=1);
              VALID(Buscar(oQryVend,oDlgR,oGet1[3],oGet1[4]));
              ACTION (oGet1[3]:cText:= 0, Buscar(oQryVend,oDlgR,oGet1[3],oGet1[4])) BITMAP "BUSC1" 
 REDEFINE GET oGet1[4] VAR cNomVen ID 104 OF oDlgR PICTURE "@!" WHEN(.F.)

 REDEFINE BUTTON oBot1[1] ID 300 OF oDlgR ACTION (lrta:=.t.,oDlgR:End()) 
 REDEFINE BUTTON oBot1[2] ID 301 OF oDlgR ACTION oDlgR:End()

ACTIVATE DIALOG oDlgR CENTER //ON INIT oGet1[01]:SetFocus()
IF !lRta 
   RETURN .f.
ENDIF
cTipo:= IF(nTipo=1,"I","E")
RETURN .t.



STATIC FUNCTION ArmaPLan(oGet1,oBrw2,oQryCuo)
LOCAL i, nCuotas := oGet1[09]:value, nValor := ROUND(oGet1[06]:value / nCuotas,2),;
      dFecVto := oGet1[10]:value, nAcu := 0, oRow, aLimites := {31,28,31,30,31,30,31,31,30,31,30,31}
oQryCuo:Zap()
FOR i := 1 TO nCuotas
    oRow := oQryCuo:GetBlankRow()
    oRow:cuota := i
    oRow:fecvto := dFecVto
    IF i = nCuotas
       oRow:importe := oGet1[06]:value - nAcu
       ELSE
       oRow:importe := nValor
    ENDIF   
    nAcu := nAcu + nValor
    dFecVto := dFecVto + aLimites[MONTH(dFecVto)]
    oQryCuo:oRow := oRow
    oQryCuo:Save()
    oQryCuo:Refresh()
NEXT i    
oBrw2:Refresh()
oBrw2:MakeTotals()
RETURN nil


*****************************************************************************
*** Imprimir factura electronica
*******************************************
** Impresion de factura
FUNCTION PrintFactuElec(cTipoDoc,cNumComp)
LOCAL aiva := { "1 IVA Responsable Inscripto","2 IVA Responsable no Inscripto",;
                "3 IVA no Responsable","4 IVA Sujeto Exento",;
                "5 Consumidor Final","6 Responsable Monotributo",;
                "7 Sujeto no Categorizado","8 Proveedor del Exterior",;
                "9 Cliente del Exterior","10  IVA Liberado – Ley Nº 19.640",;
                "11  IVA Responsable Inscripto – Agente de Percepción",;
                "12  Pequeño Contribuyente Eventual","13  Monotributista Social",;
                "14  Pequeño Contribuyente Eventual Social"},;
      i, x, y, oPrn, nRow, oFont, oFont4, oQryVen1, oQryDet1, config, oQryIvaDet
   oQryVen1 := oApp:oServer:Query("SELECT * FROM ge_"+oApp:cId+"ventas_encab WHERE ticomp = " + ClipValue2Sql(cTipoDoc)+;
                                 " AND letra = "+Clipvalue2sql(LEFT(cNumComp,1))+;
                                 " AND numcomp = " + ClipValue2Sql(RIGHT(cNumComp,13)))
   IF oQryVen1:nRecCount = 0
      MsgStop("Factura no existe!!","Error")
      RETURN nil
   ENDIF   
   IF EMPTY(oQryVen1:cae)
      MsgStop("Factura Sin CAE!!","Error")
      RETURN nil
   ENDIF   
   oQryIvaDet:= oApp:oServer:Query("SELECT SUM(v.iva) AS iva,SUM(v.neto) AS neto, v.codiva, i.nombre AS nomiva FROM ge_"+oApp:cId+"ventas_det v "+;
                                   "INNER JOIN ge_"+oApp:cId+"ivas i ON v.codiva = i.codigo "+;
                                   "WHERE v.nrofac = " + ClipValue2Sql(cTipoDoc+cNumComp)+ " GROUP BY v.codiva ")
   oQryDet1 := oApp:oServer:Query("SELECT * FROM ge_"+oApp:cId+"ventas_det WHERE nrofac = " + ClipValue2Sql(cTipoDoc+cNumComp))
   config   := oApp:oServer:Query("SELECT * FROM ge_"+oApp:cId+"config")
   ** FACTURACION CON FORMATO PARA FACTURA ELECTRONICA
   DEFINE FONT oFont NAME "COURIER NEW" SIZE -config:fon,config:fon*2
   AddFontResource( "i2of5txt.ttf" )
   DEFINE FONT oFont4 NAME "Interleaved 2of5 Text"    SIZE -45, 170 
   PRINT oPrn NAME "Factura" PREVIEW MODAL
   FOR x := 1 TO 2
   PAGE
   nRow := oPrn:nVertRes() / 66
   oPrn:SayImage(0,0, IF(LEFT(cNumComp,1)="B","b.jpg",IF(LEFT(cNumComp,1)="A","a.jpg","c.jpg")),;
                      oPrn:nHorzRes(), oPrn:nVertRes() ,   nil, .t. )
   oPrn:SayImage(nRow*.5,nRow*.5, "logo.jpg",nRow*14, nRow*8 ,   nil, .t. )   
       IF cTipoDoc <> "FC"          
          oPrn:Say( config:x1,config:y1 , IF(cTipoDoc=="NC","NOTA DE CREDITO "+oQryVen1:tipfor,;
                                                       "NOTA DE DEBITO "+oQryVen1:tipfor),oFont)
          ELSE
          oPrn:Say( config:x1,config:y1 ,"FACTURA "+oQryVen1:tipfor,oFont)
       ENDIF
       oPrn:Say( config:x2,config:y2 , "Comprobante Nro:"+cNumComp,oFont )
       oPrn:Say( config:x3,config:y3 , "Fecha Factura:" + DTOC(oQryVen1:fecha),oFont)
       oPrn:Say( config:x3+nRow+5,config:y3 , IF(x=1,"ORIGINAL","DUPLICADO"),oFont)
       oPrn:Say( config:x4,config:y4 , "("+STR(oQryVen1:codcli,5)+")  "+oQryVen1:nombre,oFont)
       oPrn:Say( config:x5,config:y5 , ALLTRIM(oQryVen1:direccion) + " " + ;
                                         ALLTRIM(oQryVen1:localidad) ,oFont)
       oPrn:Say( config:x7,config:y7 , "C.U.I.T.:"+oQryVen1:cuit+" DNI:"+STR(oQryVen1:dni),oFont)
       oPrn:Say( config:x6,config:y6 , aIva[oQryVen1:coniva] + oQryVen1:observa,oFont)
       y := config:desdey
       oQryDet1:GoTop()
       FOR i = 1 TO oQryDet1:nRecCount           
           oPrn:Say( y ,  10 ,  STR(oQryDet1:codart,13)+ ;
                                STR(oQryDet1:cantidad,08,2) + ;
                                " " + LEFT(oQryDet1:detart,49) + " " +;
                                IF(LEFT(cNumComp,1) <> "A",;
                                   STR((oQryDet1:neton*1.21)/oQryDet1:cantidad,8,3),;
                                   STR(oQryDet1:neton/oQryDet1:cantidad ,8,3)) + " " + ;
                                IF(LEFT(cNumComp,1) <> "A",;
                                   STR((oQryDet1:neton*1.21),9,2),;
                                   STR(oQryDet1:neton,9,2)),oFont)           
           oQryDet1:Skip()
           y := y + nRow + 5
       NEXT       
       y = y + nRow + 5
       oPrn:Say( y ,  10 ,  oQryVen1:observa,oFont)       
       IF LEFT(cNumComp,1)<>"A"
          oPrn:Say( config:x9,config:y9,STR(oQryVen1:importe,10,2),oFont)
          oPrn:Say( config:x9+5+nRow, config:ya, "CAE:"+oQryVen1:cae+ " Vto:"+DTOC(oQryVen1:fecVto) ,oFont)
          oPrn:Say( config:x9+10+nRow*2,config:ya,;
                       CodigoBarra(STRTRAN(oApp:cuit_emp,"-","")+ oQryVen1:tipfor+;
                       SUBSTR(oQryVen1:numcomp,1,4)+ALLTRIM(oQryVen1:cae)+DTOS(oQryVen1:fecVto) ),oFont4)
          ELSE
          *oPrn:Say( config:xb- (config:x5-config:x4),config:yb, ;
          *          ivas04:nombre,oFont)
          oPrn:Say( config:xa,config:ya, STR(oQryVen1:neto,10,2),oFont)
          oPrn:Say( config:xb,config:yb, STR(oQryVen1:iva,10,2),oFont)          
          oPrn:Say( config:x9,config:y9, STR(oQryVen1:importe,10,2),oFont)
          oPrn:Say( config:x9+5+nRow, config:ya, "CAE:"+oQryVen1:cae+ " Vto:"+DTOC(oQryVen1:fecVto) ,oFont)
          oPrn:Say( config:x9+10+nRow*2,config:ya,;
                       CodigoBarra(STRTRAN(oApp:cuit_emp,"-","")+ oQryVen1:tipfor+;
                       SUBSTR(oQryVen1:numcomp,1,4)+ALLTRIM(oQryVen1:cae)+DTOS(oQryVen1:fecVto) ),oFont4)
       ENDIF
   ENDPAGE
   NEXT x
   ENDPRINT
RETURN nil

STATIC FUNCTION CodigoBarra( x )
LOCAL i, bar := {}, j := 0, bar1 := {}, cBarr := ""
x := x + DigitoVer(x)
FOR i := 48 TO 97
       AADD(bar ,CHR(i))
       AADD(bar1,STRTRAN(STR(j,2)," ","0"))
       j++
   NEXT i
   FOR i := 192 TO 241
       AADD(bar,CHR(i))
       AADD(bar1,STRTRAN(STR(j,2)," ","0"))
       j++
   NEXT i
FOR j := 1 TO LEN(x)-1 STEP 2
    i := ASCAN(bar1,SUBSTR(x,j,2))
    cBarr := cBarr + bar[i]
NEXT j
RETURN cBarr

STATIC FUNCTION DigitoVer(cText)
LOCAL nSuma := 0, i, aSecuencia := {1,3,5,7,9,3,5,7,9,3,5,7,9,3,5,7,9,3,5,7,9,3,5,7,9,3,5,7,9,3,5,7,9,3,5,7,9,3,5,7,9,3,5,7,9}
FOR i := 1 TO LEN(cText)
    nSuma := nSuma + VAL(SUBSTR(cText,i,1)) * aSecuencia[i]       
NEXT I
nSuma := INT(nSuma / 2)
nSuma := nSuma%10
RETURN STR(nSuma,1)



*************************************************
** Agregar cheques de Tercero
STATIC FUNCTION AddCheqT(oWnd)
LOCAL oGet2 := ARRAY(12), oBot2 := ARRAY(2) , oDlg3, cNomCue := SPACE(30), cNomPro := SPACE(30),;
      mrta := .f., aCor, oQryChe1, base, oError, oFont,oQryBan
DO WHILE .T.
base := oQryChe:GetBlankRow()
base:tipo  := 1
base:recibo  := oApp:oServer:GetAutoIncrement("ge_"+oApp:cId+"pagos")
base:importe := IF(nCtaCte>0,nCtaCte,0)
base:codcli  := ncodcli
base:fecemi  := DATE()
base:fecvto  := DATE()
cNomPro      := oQryCli:nombre
DEFINE FONT oFont NAME "TAHOMA" SIZE 0,-11.5
oQryBan:= oApp:oServer:Query("SELECT * FROM ge_"+oApp:cId+"bancos")

DEFINE DIALOG oDlg3 TITLE "Alta de Cheques de Tercero" FROM 05,15 TO 23,66 OF oWnd FONT oFont
   acor := AcepCanc(oDlg3)
   @ 07, 05 SAY "Banco:"         OF oDlg3 PIXEL SIZE 40,12 RIGHT
   @ 22, 05 SAY "Numero:"        OF oDlg3 PIXEL SIZE 40,12 RIGHT
   @ 37, 05 SAY "Entregado por:" OF oDlg3 PIXEL SIZE 40,12 RIGHT
   @ 52, 05 SAY "En recibo:"     OF oDlg3 PIXEL SIZE 40,12 RIGHT
   @ 67, 05 SAY "Emision:"       OF oDlg3 PIXEL SIZE 40,12 RIGHT
   @ 82, 05 SAY "Vencimiento:"   OF oDlg3 PIXEL SIZE 40,12 RIGHT
   @ 97, 05 SAY "Importe:"       OF oDlg3 PIXEL SIZE 40,12 RIGHT
  
   @ 05, 50 GET oGet2[1] VAR base:numban  PICTURE "999" OF oDlg3 PIXEL SIZE 25,12 RIGHT;
                VALID(Buscar(oQryBan,oDlg3,oGet2[1],oGet2[2]) );
                ACTION (oGet2[1]:cText:= 0, Buscar(oQryBan,oDlg3,oGet2[1],oGet2[2])) BITMAP "BUSC1"         
   @ 05, 80 GET oGet2[2] VAR cNomCue OF oDlg3 PIXEL PICTURE "@!" WHEN(.F.)   
   @ 20, 50 GET oGet2[3] VAR base:numche  PICTURE "9999999999"  OF oDlg3 PIXEL ;
                VALID(base:numche > 0) RIGHT
   @ 35, 50 GET oGet2[4] VAR base:codcli OF oDlg3  PICTURE "999999" PIXEL RIGHT WHEN(.F.)
   @ 35, 80 GET oGet2[5] VAR cNomPro OF oDlg3 SIZE 110,12  PIXEL WHEN(.F.)
   @ 50, 50 GET oGet2[6] VAR base:recibo PICTURE "99999999" PIXEL OF oDlg3 RIGHT WHEN(.F.)
   @ 65, 50 GET oGet2[7] VAR base:fecemi   PICTURE "@D" PIXEL OF oDlg3 CENTER
   @ 80, 50 GET oGet2[8] VAR base:fecvto   PICTURE "@D" PIXEL OF oDlg3 VALID(base:fecvto >= base:fecemi) CENTER
   @ 95, 50 GET oGet2[9] VAR base:importe  PICTURE "9999999.99" PIXEL OF oDlg3 RIGHT VALID(base:importe>0)
   @ acor[1],acor[2] BUTTON oBot2[1] PROMPT "&Grabar" OF oDlg3 SIZE 30,10 ;
           ACTION ((mrta := .t.), oDlg3:End() ) PIXEL
   @ acor[3],acor[4] BUTTON oBot2[2] PROMPT "&Cancelar" OF oDlg3 SIZE 30,10 ;
           ACTION ((mrta := .f.), oDlg3:End() ) PIXEL CANCEL
ACTIVATE DIALOG oDlg3 CENTER ON INIT oGet2[1]:SetFocus()
IF !mRta
   RETURN nil
ENDIF
oQryChe1 := oApp:oServer:Query("SELECT * FROM ge_"+oApp:cId+"cheter WHERE numban = " + ClipValue2Sql(base:numban) +;
                               " AND numche = " + ClipValue2Sql(base:numche) )
IF oQryChe1:nRecCount > 0
   MsgStop("Cheque ya existe", "Error")
   LOOP
ENDIF   
oQryChe:oRow := base
TRY
  oApp:oServer:BeginTransaction()
  oQryChe:Save()
  oQryChe:Refresh()
  oApp:oServer:CommitTransaction()
CATCH oError
    ValidaError(oError)
  LOOP
END TRY
EXIT
ENDDO
RETURN nil

*******************************************
** Impresion de factura
FUNCTION PrintPresupu(oQryDet,oGet,oBrw,lDolar)
LOCAL i, x, y, oPrn, nRow, oFont, oFont4, config   
   config   := oApp:oServer:Query("SELECT * FROM ge_"+oApp:cId+"config")
   ** FACTURACION CON FORMATO PARA PRESUPUESTO
   DEFINE FONT oFont NAME "COURIER NEW" SIZE -config:fon,config:fon*2
   PRINT oPrn NAME "Presupuesto" PREVIEW MODAL
   FOR x := 1 TO 1
   PAGE
   nRow := oPrn:nVertRes() / 66
   oPrn:SayImage(0,0, "X.jpg",oPrn:nHorzRes(), oPrn:nVertRes() ,   nil, .t. )              
       oPrn:Say( config:x3,config:y3 , "Fecha:" + DTOC(DATE()),oFont)
       oPrn:Say( config:x4,config:y4 , "("+STR(oGet[8]:value,5)+")  "+oGet[9]:cText,oFont)
       oPrn:Say( config:x5,config:y5 , ALLTRIM(oGet[10]:cText) + " " + ;
                                         ALLTRIM(oGet[11]:cText) ,oFont)
       oPrn:Say( config:x7,config:y7 , "C.U.I.T.:"+oGet[12]:cText,oFont)
       oPrn:Say( config:x6,config:y6 , "Observaciones:"+oGet[23]:cText,oFont)
       y := config:desdey
       IF lDolar 
          oPrn:Say( y ,  10 ,"(Precios expresados en u$s)",oFont)
          y := y + nRow + 5
       ENDIF
       oQryDet:GoTop()
       FOR i = 1 TO oQryDet:nRecCount           
           oPrn:Say( y ,  10 ,  STR(oQryDet:codart,13)+ ;
                                STR(oQryDet:cantidad,08,2) + ;
                                " " + LEFT(oQryDet:detart,49) + " " +;
                                STR(oQryDet:punit/IF(lDolar,oApp:dolar,1),10,2) +;
                                STR(oQryDet:ptotal/IF(lDolar,oApp:dolar,1),10,2),oFont)           
           oQryDet:Skip()
           y := y + nRow + 5
       NEXT       
       y = y + nRow + 5                    
       oPrn:Say( config:x9,config:y9, STR(oGet[21]:value/IF(lDolar,oApp:dolar,1),12,2),oFont)
   ENDPAGE
   NEXT x
   ENDPRINT
RETURN nil


*******************************************
** Impresion de factura
FUNCTION PrintRemito(oQryDet,oGet,oBrw, cNumComp)
LOCAL i, x, y, oPrn, nRow, oFont, oFont4, config   
   config   := oApp:oServer:Query("SELECT * FROM ge_"+oApp:cId+"config")
   ** FACTURACION CON FORMATO PARA PRESUPUESTO
   DEFINE FONT oFont NAME "COURIER NEW" SIZE -config:fon,config:fon*2
   PRINT oPrn NAME "Remito" PREVIEW MODAL
   FOR x := 1 TO 1
   PAGE
   nRow := oPrn:nVertRes() / 66
   oPrn:SayImage(0,0, "R.jpg",oPrn:nHorzRes(), oPrn:nVertRes() ,   nil, .t. )              
   oPrn:SayImage(nRow*.5,nRow*.5, "logo.jpg",nRow*14, nRow*8 ,   nil, .t. )   
       oPrn:Say( config:x2,config:y2 , "Remito Nro:"+cNumComp,oFont ) 
       oPrn:Say( config:x3,config:y3 , "Fecha:" + DTOC(DATE()),oFont)
       oPrn:Say( config:x4,config:y4 , "("+STR(oGet[8]:value,5)+")  "+oGet[9]:cText,oFont)
       oPrn:Say( config:x5,config:y5 , ALLTRIM(oGet[10]:cText) + " " + ;
                                         ALLTRIM(oGet[11]:cText) ,oFont)
       oPrn:Say( config:x7,config:y7 , "C.U.I.T.:"+oGet[12]:cText,oFont)
       oPrn:Say( config:x6,config:y6 , "Observaciones:"+oGet[23]:cText,oFont)
       y := config:desdey
       oQryDet:GoTop()
       FOR i = 1 TO oQryDet:nRecCount           
           oPrn:Say( y ,  10 ,  STR(oQryDet:codart,13)+ ;
                                STR(oQryDet:cantidad,08,2) + ;
                                " " + LEFT(oQryDet:detart,49) + " ";
                                ,oFont)           
           oQryDet:Skip()
           y := y + nRow + 5
       NEXT       
       y = y + nRow + 5                    
       *oPrn:Say( config:x9,config:y9, oGet[21]:cText,oFont)
   ENDPAGE
   NEXT x
   ENDPRINT
RETURN nil
